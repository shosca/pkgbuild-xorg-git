# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: M Rawash <mrawash@gmail.com>

_gitroot=git://git.code.sf.net/p/linuxwacom/xf86-input-wacom
_gitname=repo-xf86-input-wacom

pkgname=xf86-input-wacom
pkgver=0.32.0.5.r1874.g3b0c093
pkgrel=23
pkgdesc="X.Org Wacom tablet driver"
arch=(i686 x86_64)
url="http://linuxwacom.sourceforge.net/"
license=(GPL)
depends=(libxi libxinerama libxrandr systemd)
makedepends=('git' 'xorg-server-devel>1.1.8.98' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.18.98' 'X-ABI-XINPUT_VERSION<22.1' 'X-ABI-XINPUT_VERSION>=23')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
