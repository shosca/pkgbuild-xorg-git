# $Id: PKGBUILD 154156 2012-03-23 20:01:59Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://anongit.freedesktop.org/xorg/app/xkbcomp'
_gitname='repo-xkbcomp'

pkgname=xorg-xkbcomp
pkgver=1.3.1.2.r162.g8995ac5
pkgrel=3
pkgdesc="X Keyboard description compiler"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros' 'git')
groups=('xorg-apps' 'xorg')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2-3 | tr - .)" "$(git rev-list HEAD --count)" "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

