# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

pkgbase=mesa-git
pkgver=r65005.e34a363
pkgrel=1
pkgname=('ati-dri-git' 'intel-dri-git' 'nouveau-dri-git' 'svga-dri-git' 'opencl-mesa-git' 'mesa-git' 'mesa-libgl-git')

arch=('i686' 'x86_64')
makedepends=('git' 'python2' 'libxml2' 'libx11-git' 'glproto-git' 'libdrm-git'
             'dri2proto-git' 'dri3proto-git' 'presentproto-git' 'libclc-git'
             'libxshmfence-git' 'libomxil-bellagio-git' 'libxxf86vm-git'
             'libxdamage-git' 'libvdpau-git' 'wayland-git' 'elfutils'
             'llvm-git' 'systemd' 'opencl-headers')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=($_gitroot LICENSE)
sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-gallium-egl \
    --enable-gallium-llvm \
    --enable-gallium-osmesa \
    --enable-gallium-gbm \
    --enable-egl \
    --with-egl-platforms=drm,x11,wayland \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --with-llvm-shared-libs \
    --enable-r600-llvm-compiler \
    --enable-opencl \
    --enable-opencl-icd \
    --enable-omx

  make

  msg 'Starting fake install..'
  rm -rf $srcdir/fakeinstall
  mkdir $srcdir/fakeinstall
  make -j1 DESTDIR=${srcdir}/fakeinstall install
}

package_ati-dri-git() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  depends=('mesa-libgl-git' 'libclc-git' 'libomxil-bellagio-git' 'libtxc_dxtn' 'mesa-git')
  provides=('ati-dri')
  conflicts=('ati-dri')
  replaces=('ati-dri')

  install -m755 -d ${pkgdir}/usr/lib/
  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  install -m755 -d ${pkgdir}/usr/lib/dri/
  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe/
  install -m755 -d ${pkgdir}/usr/lib/bellagio/

  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_{r600,radeonsi}.* ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib/dri/
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_{r300,r600,radeonsi}* ${pkgdir}/usr/lib/gallium-pipe/
  #mv -v ${srcdir}/fakeinstall/usr/lib/bellagio/libomx_{r600,radeonsi}* ${pkgdir}/usr/lib/bellagio/
  #mv -v ${srcdir}/fakeinstall/usr/lib/libXvMCr600.* ${pkgdir}/usr/lib/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_intel-dri-git() {
  pkgdesc="Mesa drivers for Intel"
  depends=('mesa-libgl-git' 'libtxc_dxtn' 'mesa-git')
  provides=('intel-dri')
  replaces=('intel-dri')
  conflicts=('intel-dri')

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_nouveau-dri-git() {
  pkgdesc="Mesa drivers for Nouveau"
  depends=('mesa-libgl-git' 'libtxc_dxtn' 'mesa-git')
  provides=('nouveau-dri')
  replaces=('nouveau-dri')
  conflicts=('nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  install -m755 -d ${pkgdir}/usr/lib/dri
  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe

  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_nouveau.* ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib/dri/

  #mv -v ${srcdir}/fakeinstall/usr/lib/libXvMCnouveau* ${pkgdir}/usr/lib/gallium-pipe/
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_svga-dri-git() {
  pkgdesc="Gallium3D VMware guest GL driver"
  depends=('libdrm-git' 'expat' 'llvm-libs-git' 'mesa-git')
  provides=('svga-dri')
  replaces=('svga-dri')
  conflicts=('svga-dri')

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/vmwgfx_dri.so ${pkgdir}/usr/lib/dri/

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_vmwgfx* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_opencl-mesa-git() {
  pkgdesc="Mesa OpenCL library"
  depends=('mesa-git' 'llvm-git' 'libclc-git' 'opencl-headers')
  provides=("opencl-mesa" "libcl")
  replaces=('opencl-mesa' 'libcl')
  conflicts=('libcl')

  install -m755 -d ${pkgdir}/etc
  mv -v ${srcdir}/fakeinstall/etc/OpenCL ${pkgdir}/etc

  install -m755 -d "${pkgdir}/usr/lib/"
  mv -v ${srcdir}/fakeinstall/usr/lib/lib*OpenCL* ${pkgdir}/usr/lib/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}


package_mesa-git() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm-git' 'libvdpau-git' 'wayland-git' 'libxshmfence-git'
           'libxxf86vm-git' 'libxdamage-git' 'systemd' 'llvm-libs-git')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  install -m755 -d ${pkgdir}/usr/lib/mesa
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv -v ${pkgdir}/usr/lib/libGL.so*    ${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libEGL.so*   ${pkgdir}/usr/lib/mesa/
  mv -v ${pkgdir}/usr/lib/libGLES*.so* ${pkgdir}/usr/lib/mesa/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-libgl-git(){
  pkgdesc="Mesa 3-D graphics library"
  depends=('mesa-git')
  provides=('mesa-libgl' 'libgl')
  replaces=('mesa-libgl' 'libgl')
  conflicts=('mesa-libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s /usr/lib/mesa/libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so.1.2.0
  ln -s libGL.so.1.2.0               ${pkgdir}/usr/lib/libGL.so.1
  ln -s libGL.so.1.2.0               ${pkgdir}/usr/lib/libGL.so

  ln -s /usr/lib/mesa/libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0               ${pkgdir}/usr/lib/libEGL.so.1
  ln -s libEGL.so.1.0.0               ${pkgdir}/usr/lib/libEGL.so

  ln -s /usr/lib/mesa/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0               ${pkgdir}/usr/lib/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0               ${pkgdir}/usr/lib/libGLESv1_CM.so

  ln -s /usr/lib/mesa/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0               ${pkgdir}/usr/lib/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0               ${pkgdir}/usr/lib/libGLESv2.so

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
