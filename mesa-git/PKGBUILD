# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

pkgbase=mesa-git
pkgver=r66474.5e37a2a
pkgrel=1
pkgname=('opencl-mesa-git' 'mesa-dri-git' 'mesa-vdpau-git' 'mesa-git' 'mesa-libgl-git')
arch=('i686' 'x86_64')
makedepends=('git' 'python2' 'libxml2' 'libx11-git' 'glproto-git' 'libdrm-git'
             'dri2proto-git' 'dri3proto-git' 'presentproto-git' 'libclc-git'
             'libxshmfence-git' 'libomxil-bellagio-git' 'libxxf86vm-git'
             'libxdamage-git' 'libvdpau-git' 'wayland-git' 'elfutils'
             'llvm-git' 'systemd' 'opencl-headers' 'clang-git')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=($_gitroot LICENSE)
sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-egl-platforms=drm,x11,wayland \
    --with-llvm-shared-libs \
    --enable-egl \
    --disable-gallium-egl \
    --disable-gallium-gbm \
    --enable-gbm \
    --enable-gallium-llvm \
    --enable-shared-glapi \
    --enable-glx \
    --enable-glx-tls \
    --enable-dri \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-omx \
    --enable-opencl \
    --enable-opencl-icd \
    --with-clang-libdir=/usr/lib

  make

  rm -rf $srcdir/fakeinstall
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_opencl-mesa-git() {
  pkgdesc="OpenCL support for AMD/ATI Radeon mesa drivers"
  depends=('expat' 'libdrm-git' 'elfutils' 'libxfixes-git' 'libxext-git' 'libcl' 'libclc-git' 'clang-git')
  provides=('opencl-mesa')
  replaces=('opencl-mesa')
  conflicts=('opencl-mesa')

  install -m755 -d ${pkgdir}/etc
  mv -v ${srcdir}/fakeinstall/etc/OpenCL ${pkgdir}/etc

  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/lib*OpenCL* ${pkgdir}/usr/lib/
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_{r600,radeonsi}.so ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-dri-git() {
  pkgdesc="Mesa DRI drivers"
  depends=('expat' 'libdrm-git' 'mesa-git' 'libtxc_dxtn')
  conflicts=('mesa-dri' 'ati-dri' 'ati-dri-git' 'intel-dri' 'intel-dri-git' 'nouveau-dri' 'nouveau-dri-git' 'svga-dri' 'svga-dri-git')
  provides=('mesa-dri' 'ati-dri' 'ati-dri-git' 'intel-dri' 'intel-dri-git' 'nouveau-dri' 'nouveau-dri-git' 'svga-dri' 'svga-dri-git')
  replaces=('mesa-dri' 'ati-dri' 'ati-dri-git' 'intel-dri' 'intel-dri-git' 'nouveau-dri' 'nouveau-dri-git' 'svga-dri' 'svga-dri-git')

  install -m755 -d ${pkgdir}/etc
  mv -v ${srcdir}/fakeinstall/etc/drirc ${pkgdir}/etc

  install -m755 -d ${pkgdir}/usr/lib/dri
  # ati-dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{r200,r300,r600,radeon{,si}}_dri.so ${pkgdir}/usr/lib/dri
  # nouveau-dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/nouveau{,_vieux}_dri.so ${pkgdir}/usr/lib/dri
  # intel-dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/dri
  # svga-dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/vmwgfx_dri.so ${pkgdir}/usr/lib/dri
  # swrast
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{kms_,}swrast_dri.so ${pkgdir}/usr/lib/dri

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-vdpau-git() {
  pkgdesc="Mesa VDPAU drivers"
  depends=('libdrm-git' 'libx11-git' 'mesa-git' 'expat' 'elfutils')
  conflicts=('mesa-vdpau')
  provides=('mesa-vdpau')
  replaces=('mesa-vdpau')

  install -m755 -d ${pkgdir}/usr/lib
  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau ${pkgdir}/usr/lib

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-git() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm-git' 'libvdpau-git' 'wayland-git' 'libxshmfence-git'
           'libxxf86vm-git' 'libxdamage-git' 'systemd' 'llvm-libs-git')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  install -m755 -d ${pkgdir}/usr/lib
  mv -v ${srcdir}/fakeinstall/usr/lib/bellagio ${pkgdir}/usr/lib
  mv -v ${srcdir}/fakeinstall/usr/lib/lib{OSMesa,gbm,glapi,wayland-egl,xatracker}.so* ${pkgdir}/usr/lib/

  mv -v ${srcdir}/fakeinstall/usr/include ${pkgdir}/usr
  mv -v ${srcdir}/fakeinstall/usr/lib/pkgconfig ${pkgdir}/usr/lib/

  install -m755 -d ${pkgdir}/usr/lib/mesa
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv -v ${srcdir}/fakeinstall/usr/lib/libGL.so*    ${pkgdir}/usr/lib/mesa/
  mv -v ${srcdir}/fakeinstall/usr/lib/libEGL.so*   ${pkgdir}/usr/lib/mesa/
  mv -v ${srcdir}/fakeinstall/usr/lib/libGLES*.so* ${pkgdir}/usr/lib/mesa/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-libgl-git(){
  pkgdesc="Mesa 3-D graphics library"
  depends=('mesa-git')
  provides=('mesa-libgl' 'libgl')
  replaces=('mesa-libgl' 'libgl')
  conflicts=('mesa-libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s /usr/lib/mesa/libGL.so.1.2.0 ${pkgdir}/usr/lib/libGL.so.1.2.0
  ln -s libGL.so.1.2.0               ${pkgdir}/usr/lib/libGL.so.1
  ln -s libGL.so.1.2.0               ${pkgdir}/usr/lib/libGL.so

  ln -s /usr/lib/mesa/libEGL.so.1.0.0 ${pkgdir}/usr/lib/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0               ${pkgdir}/usr/lib/libEGL.so.1
  ln -s libEGL.so.1.0.0               ${pkgdir}/usr/lib/libEGL.so

  ln -s /usr/lib/mesa/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0               ${pkgdir}/usr/lib/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0               ${pkgdir}/usr/lib/libGLESv1_CM.so

  ln -s /usr/lib/mesa/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0               ${pkgdir}/usr/lib/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0               ${pkgdir}/usr/lib/libGLESv2.so

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
