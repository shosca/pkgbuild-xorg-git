# $Id: PKGBUILD 160633 2012-06-02 21:43:25Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libX11"
_gitname="libX11"

pkgname=lib32-libx11-git
pkgver=r1257.446f5f7
pkgrel=2
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxcb-git' 'libx11-git')
makedepends=('xorg-util-macros-git' 'xextproto-git' 'xtrans-git' 'inputproto-git' 'git')
provides=('lib32-libx11')
conflicts=('lib32-libx11')
replaces=('lib32-libx11')
options=('!libtool')
license=('custom')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static \
    --disable-xf86bigfont

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

