# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

_gitroot="git://github.com/xkbcommon/libxkbcommon.git"
_gitname="repo-libxkbcommon"

pkgbase=libxkbcommon
pkgname=(libxkbcommon libxkbcommon-x11)
pkgver=0.7.1.0.r1608.g877fe59
pkgrel=1
pkgdesc="Keyboard handling library using XKB data"
arch=(i686 x86_64)
url="http://xkbcommon.org/"
license=(custom)
depends=(xkeyboard-config glibc)
checkdepends=(xorg-server-xvfb xorg-xauth libgl)
makedepends=(git libxcb doxygen xorg-util-macros)
source=("$_gitname::$_gitroot" LICENSE)
sha256sums=('SKIP'
            '41489198d8b7cef9e0fd80f37d501d68cfcee7bec079ac7f5d26a0f552a1ca19')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,lib/pkgconfig}
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${_gitname}"
  make -k check
}

package_libxkbcommon() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/libxkbcommon/LICENSE"

### Split libxkbcommon-x11

  mv "$pkgdir"/usr/lib/*x11* "$srcdir/x11/usr/lib"
  mv "$pkgdir"/usr/lib/pkgconfig/*x11* "$srcdir/x11/usr/lib/pkgconfig"
  mv "$pkgdir"/usr/include/xkbcommon/*x11* "$srcdir/x11/usr/include/xkbcommon"
}

package_libxkbcommon-x11() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients"
  depends=(libxkbcommon libxcb)

  mv x11/* "$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/libxkbcommon-x11/LICENSE"
}

# vim: ft=sh syn=sh et
