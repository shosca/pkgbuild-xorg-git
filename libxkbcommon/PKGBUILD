# Maintainer: Joel Teichroeb <joel@teichroeb.net>

_gitroot="git://github.com/xkbcommon/libxkbcommon.git"
_gitname="repo-libxkbcommon"

pkgbase=libxkbcommon
pkgname=('libxkbcommon' 'libxkbcommon-x11')
pkgver=0.6.0.1.r1572.gb91584a
pkgrel=4
pkgdesc="A library to handle keyboard descriptions."
url="http://xkbcommon.org/"
arch=('i686' 'x86_64')
license=('custom')
depends=('xkeyboard-config')
makedepends=('git' 'doxygen' 'xorg-util-macros' 'libxcb')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,lib/pkgconfig}
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package_libxkbcommon() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib/*x11* "$srcdir/x11/usr/lib"
  mv "${pkgdir}"/usr/lib/pkgconfig/*x11* "$srcdir/x11/usr/lib/pkgconfig"
  mv "${pkgdir}"/usr/include/xkbcommon/*x11* "$srcdir/x11/usr/include/xkbcommon"
}

package_libxkbcommon-x11() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients"
  depends=('libxkbcommon' 'libxcb')

  mv x11/* "${pkgdir}"
}
# vim: ft=sh syn=sh et

