# $Id: PKGBUILD 153609 2012-03-16 17:09:01Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXt"
_gitname="libXt"

pkgname=lib32-libxt-git
pkgver=r217.51d63a9
pkgrel=43
pkgdesc="X11 toolkit intrinsics library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libsm-git' 'lib32-libx11-git' 'libxt-git')
makedepends=('git' 'xorg-util-macros-git')
options=('!libtool')
provides=('lib32-libxt')
conflicts=('lib32-libxt')
replaces=('lib32-libxt')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ft=sh syn=sh et
