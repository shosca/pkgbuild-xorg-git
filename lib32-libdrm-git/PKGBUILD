# Arch libdrm Maintainers:
#Jan de Groot
#Andreas Radke

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

pkgname=lib32-libdrm-git
pkgver=r5269.e8c3c13
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
license=('MIT')
groups=('lib32')
depends=('lib32-libpciaccess-git' 'libdrm-git')
makedepends=('gcc-multilib' 'git' 'udev')
options=('!libtool')
url="http://dri.freedesktop.org/"
provides=('lib32-libdrm')
conflicts=('lib32-libdrm')
replaces=('lib32-libdrm')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  sed -i 's/PKG_CHECK_MODULES(PTHREADSTUBS, pthread-stubs)//' configure.ac

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-udev \
    --enable-intel \
    --enable-radeon \
    --enable-nouveau \
    --enable-vmwgfx

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

