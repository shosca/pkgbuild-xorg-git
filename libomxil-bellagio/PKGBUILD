# Maintainer: Joni Lapilainen <joni.lapilainen@gmail.com>

_gitroot="git://omxil.git.sourceforge.net/gitroot/omxil/omxil"
_gitname="repo-omxil"

pkgname=libomxil-bellagio
pkgver=0.9.3.r708.gf2c7fd9
pkgrel=3
pkgdesc="An opensource implementation of the OpenMAX Integration Layer API"
arch=('arm' 'armv7h' 'i686' 'x86_64')
url="http://omxil.sourceforge.net"
license=('LGPL')
makedepends=('doxygen' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/src/$_gitname"
  printf "%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 4)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"

  autoreconf -i -f
  sed -i 's/ -Werror//g' configure
  ./configure --prefix=/usr

  make -j1
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="${pkgdir}" install
}

