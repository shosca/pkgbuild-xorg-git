# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Fly-away <flyaaway@gmail.com>

_gitroot="git://omxil.git.sourceforge.net/gitroot/omxil/omxil"
_gitname="repo-omxil"

pkgname=libomxil-bellagio
pkgver=0.9.3.r708.gf2c7fd9
pkgrel=6
pkgdesc="An opensource implementation of the OpenMAX Integration Layer API"
arch=('i686' 'x86_64')
url="http://omxil.sourceforge.net"
license=('LGPL')
depends=('glibc')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 4)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_gitname"
  autoreconf -fiv
}

build() {
  cd "$srcdir/$_gitname"

  sed -i 's/ -Werror//g' configure
  ./configure --prefix=/usr --disable-static --docdir=/usr/share/doc/${pkgname}

  make -j1

  rm -rf "${srcdir}/fakeinstall"
  make DESTDIR="${srcdir}/fakeinstall" install
}

package() {
  cd "$srcdir/$_gitname"

  cp -rv ${srcdir}/fakeinstall/usr  ${pkgdir}
}

