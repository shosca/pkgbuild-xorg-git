# Maintainer: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: breakdown <breakdown[at]archlinux[dot]us>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: William Heinbockel <wheinbockel[at]gmail[dot]com>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-intel'
_gitname='xf86-video-intel'

pkgname=xf86-video-intel-git
pkgver=r9067.bff0272
pkgrel=2
pkgdesc='X.org Intel i810/i830/i915/945G/G965+ video drivers'
arch=('i686' 'x86_64')
url='http://intellinuxgraphics.org/'
license=('custom')
depends=('libpng' 'mesa-git' 'libxvmc-git' 'libpciaccess-git' 'libdrm-git' 'dri2proto-git' 'xcb-util-git' 'libxfixes-git' 'udev')
makedepends=('git' 'xorg-server-devel-git' 'libx11-git' 'xf86driproto-git' 'glproto-git' 'resourceproto-git')
provides=('xf86-video-intel')
conflicts=('xf86-video-intel' 'xorg-server<1.15.99')
replaces=('xf86-video-intel')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --enable-sna \
    --enable-kms-only

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install 

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ft=sh syn=sh et

