# $Id: PKGBUILD 154148 2012-03-23 17:34:14Z andyrtr $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/setxkbmap"
_gitname="repo-setxkbmap"

pkgname=xorg-setxkbmap
pkgver=1.3.1.0.r97.g352f4da
pkgrel=32
pkgdesc='Set the keyboard using the X Keyboard Extension'
url='http://xorg.freedesktop.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('libxkbfile')
makedepends=('xorg-util-macros' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')
groups=('xorg-apps' 'xorg')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2-3 | tr - .)" "$(git rev-list HEAD --count)" "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

