# $Id: PKGBUILD 154148 2012-03-23 17:34:14Z andyrtr $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/setxkbmap"
_gitname="setxkbmap"

pkgname=xorg-setxkbmap-git
pkgver=r96.c0eed7d
pkgrel=20
pkgdesc='Set the keyboard using the X Keyboard Extension'
url='http://xorg.freedesktop.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('libxkbfile-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('xorg-setxkbmap')
conflicts=('xorg-setxkbmap')
replaces=('xorg-setxkbmap')
source=($_gitroot)
sha256sums=('SKIP')
groups=('xorg-apps' 'xorg')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

