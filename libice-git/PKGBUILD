# $Id: PKGBUILD 151848 2012-03-03 13:42:06Z andyrtr $
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libICE"
_gitname="libICE"

pkgname=libice-git
pkgver=r142.0dfab42
pkgrel=7
pkgdesc="X11 Inter-Client Exchange library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'xproto-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'xtrans-git' 'git')
license=('custom')
provides=('libice')
replaces=('libice')
conflicts=('libice')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

