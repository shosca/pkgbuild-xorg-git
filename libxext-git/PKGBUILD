# $Id: PKGBUILD 152773 2012-03-09 16:10:15Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXext"
_gitname="libXext"

pkgname=libxext-git
pkgver=r198.efdcbb7
pkgrel=46
pkgdesc="X11 miscellaneous extensions library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11-git' 'xextproto-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('libxext=1.3.2')
conflicts=('libxext')
replaces=('libxext')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

