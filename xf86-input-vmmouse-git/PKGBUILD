# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-vmmouse

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-vmmouse
_gitname=xf86-input-vmmouse

pkgname=xf86-input-vmmouse-git
pkgver=r132.8081bdc
pkgrel=18
pkgdesc="X.Org vmmouse Input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/xf86-input-vmmouse/"
license=('custom')
depends=('glibc' 'libsystemd' 'sh')
makedepends=('xorg-server-devel-git' 'resourceproto-git' 'scrnsaverproto-git' 'git')
groups=('xorg-drivers' 'xorg')
provides=("xf86-input-vmmouse")
conflicts=('xf86-input-vmmouse')
replaces=('xf86-input-vmmouse')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr \
    --with-udev-rules-dir=/usr/lib/udev/rules.d
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rfv ${pkgdir}/usr/{lib,share}/hal
}
# vim: ft=sh syn=sh et

