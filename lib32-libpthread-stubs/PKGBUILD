# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Robert Wiklund <robert@wikro.org>
# Contributor: carstene1ns <arch carsten-teibes de>

_gitroot="git://anongit.freedesktop.org/xcb/pthread-stubs"
_gitname="repo-pthread-stubs"

pkgname=lib32-libpthread-stubs
pkgver=0.3.17.gfa6db2f
pkgrel=2
pkgdesc="This library provides weak aliases for pthread functions not provided in libc or otherwise available by default."
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org/dist/"
license=('MIT')
depends=('libpthread-stubs')
makedepends=('git')
source=("$_gitname::$_gitroot")
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh --prefix=/usr \
    --libdir=/usr/lib32 \

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:

