# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-vesa'
_gitname='xf86-video-vesa'

pkgname=xf86-video-vesa-git
pkgver=r148.b49c293
pkgrel=1
pkgdesc='X.org vesa video driver'
arch=('i686' 'x86_64')
url='http://xorg.freedesktop.org/'
license=('custom')
depends=('glibc')
makedepends=('git' 'xorg-server-devel-git')
provides=('xf86-video-vesa')
conflicts=('xf86-video-vesa')
replaces=('xf86-video-vesa')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install 

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ft=sh syn=sh et

