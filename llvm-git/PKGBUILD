# Maintainer: Armin K. <krejzi at email dot com>
# Contributor: Christian Babeux <christian.babeux@0x80.ca>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

_gitroot="http://llvm.org/git/llvm"
_gitname="llvm"
_pkgname=$_gitname

pkgname=(llvm-git llvm-libs-git llvm-ocaml-git clang-git clang-tools-extra-git clang-analyzer-git)
pkgver=r108229.837a7c0
pkgrel=1
pkgdesc='Low Level Virtual Machine'
arch=('i686' 'x86_64')
url="http://llvm.org"
license=('custom:University of Illinois')
makedepends=('git' 'libffi' 'python2' 'ocaml' 'python-sphinx')
options=('staticlibs' '!strip')
source=(git+$_gitroot
        git+http://llvm.org/git/clang.git
        git+http://llvm.org/git/clang-tools-extra.git
        git+http://llvm.org/git/compiler-rt.git
        llvm-Config-config.h
        llvm-Config-llvm-config.h
        fix-enable-shared.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '312574e655f9a87784ca416949c505c452b819fad3061f2cde8aced6540a19a3'
            '597dc5968c695bbdbb0eac9e8eb5117fcd2773bc91edf5ec103ecffffab8bc48'
            '7d0b549cff584bee55bd15d774ad6035efbf8b504a19a2c660dbbcc8ef14c7a6')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  git clone ${srcdir}/clang tools/clang
  git clone ${srcdir}/clang-tools-extra tools/clang/tools/extra
  git clone ${srcdir}/compiler-rt projects/compiler-rt

  sed -i 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
    Makefile.config.in

  patch -Np1 -i ../fix-enable-shared.patch

}

build() {
  cd "${srcdir}/${_pkgname}"

  CPPFLAGS+=" $(pkg-config --cflags libffi)"

  CC=gcc CXX=g++ \
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --enable-libffi \
      --enable-targets=all \
      --disable-expensive-checks \
      --disable-debug-runtime \
      --disable-assertions \
      --with-binutils-include=/usr/include \
      --with-python=/usr/bin/python2 \
      --enable-optimized

  make REQUIRES_RTTI=1

  # remove reference to srcdir
  sed -e "s:\$(LLVM_SRC_ROOT):/usr/src/llvm:" \
      -e "s:\$(LLVM_OBJ_ROOT):/usr/src/llvm:" \
      -i tools/llvm-config/Makefile

  make REQUIRES_RTTI=1
  make -C docs -f Makefile.sphinx man
  make -C docs -f Makefile.sphinx html
  make -C tools/clang/docs -f Makefile.sphinx html
}

package_llvm-git() {
  pkgdesc="Low Level Virtual Machine"
  depends=('llvm-libs-git' 'perl')
  provides=('llvm')
  conflicts=('llvm')
  replaces=('llvm')

  cd "${srcdir}/${_pkgname}"

  mv tools/clang "${srcdir}/clang.src"
  make -j1 DESTDIR="${pkgdir}" install
  mv "${srcdir}/clang.src" tools/clang

  rm -rf "${srcdir}"/*.so
  mv "${pkgdir}"/usr/lib/libLLVM-*.so "${srcdir}"

  rm -rf "${srcdir}"/{ocaml,ocamldoc}
  mv "${pkgdir}"/usr/{lib/ocaml,share/doc/llvm/ocamldoc} "${srcdir}"

  rm -rf "${pkgdir}"/usr/{lib/libllvm*,share/doc/llvm/ocamldoc.tar.gz}
  rm -rf "${pkgdir}"/usr/lib/*LLVMHello.*

  chmod -x "${pkgdir}"/usr/lib/*.a

  # Remove reference to pkgdir
  sed -e "s:${pkgdir}::g" -i "${pkgdir}/usr/share/llvm/cmake/LLVMConfig.cmake"

  # Symlink LLVMgold.so into /usr/lib/bfd-plugins
  # (https://bugs.archlinux.org/task/28479)
  install -d "${pkgdir}/usr/lib/bfd-plugins"
  ln -s ../LLVMgold.so "${pkgdir}/usr/lib/bfd-plugins/LLVMgold.so"

  if [[ $CARCH == x86_64 ]]; then
    # Needed for multilib (https://bugs.archlinux.org/task/29951)
    # Header stubs are taken from Fedora
    for _header in config llvm-config; do
      mv "${pkgdir}/usr/include/llvm/Config/${_header}"{,-64}.h
      cp "${srcdir}/llvm-Config-${_header}.h" \
           "${pkgdir}/usr/include/llvm/Config/${_header}.h"
    done
  fi

  install -d "${pkgdir}/usr/share/man/man1"
  cp docs/_build/man/*.1 "${pkgdir}/usr/share/man/man1/"

  cp -r docs/_build/html/* "${pkgdir}/usr/share/doc/$_pkgname/html/"
  rm -r "${pkgdir}/usr/share/doc/${_pkgname}/html/_sources"

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_llvm-libs-git() {
  pkgdesc="Low Level Virtual Machine (runtime library)"
  depends=('gcc-libs' 'zlib' 'libffi')
  provides=('llvm-libs')
  conflicts=('llvm-libs')
  replaces=('llvm-libs')

  mkdir -p "${pkgdir}"/usr/lib/

  mv "${srcdir}"/libLLVM-*.so "${pkgdir}/usr/lib/"

  for _shlib in "${pkgdir}"/usr/lib/*.so
  do
    test -L "${_shlib}" && ln -sf $(basename $(readlink ${_shlib})) ${_shlib}
  done

  install -Dm644 "${srcdir}/${_pkgname}/LICENSE.TXT" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_llvm-ocaml-git() {
  pkgdesc="OCaml bindings for LLVM"
  depends=('llvm-git' 'ocaml')
  provides=('llvm-ocaml')
  conflicts=('llvm-ocaml')
  replaces=('llvm-ocaml')

  cd "${srcdir}/${_pkgname}"

  install -d "${pkgdir}"/{usr/lib,usr/share/doc/llvm}
  cp -r "${srcdir}/ocaml" "${pkgdir}/usr/lib"
  cp -r "${srcdir}/ocamldoc" "${pkgdir}/usr/share/doc/llvm"

  chmod -x "${pkgdir}"/usr/lib/ocaml/libllvm*.a

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang-git() {
  pkgdesc="C language family frontend for LLVM"
  url="http://clang.llvm.org/"
  depends=('llvm-git' 'gcc')
  provides=('clang')
  conflicts=('clang')
  replaces=('clang')

  sed -e 's:$(PROJ_prefix)/share/doc/llvm:$(PROJ_prefix)/share/doc/clang:' \
      -i "${srcdir}/llvm/Makefile.config"

  cd "${srcdir}/llvm/tools/clang"

  mv tools/extra "${srcdir}"

  make DESTDIR="${pkgdir}" install
  mv "${srcdir}/extra" tools/

  chmod -x "${pkgdir}"/usr/lib/*.a

  sed -e 's:$(PROJ_prefix)/share/doc/clang:$(PROJ_prefix)/share/doc/llvm:' \
      -i "${srcdir}/llvm/Makefile.config"

  cp -r docs/_build/html/* "${pkgdir}/usr/share/doc/clang/html/"
  rm -r "${pkgdir}/usr/share/doc/clang/html/_sources"

  install -d "${pkgdir}/usr/lib/python2.7/site-packages"
  cp -r bindings/python/clang "${pkgdir}/usr/lib/python2.7/site-packages/"
  python2 -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/clang"
  python2 -O -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/clang"

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang-analyzer-git() {
  pkgdesc="A source code analysis framework"
  url="http://clang-analyzer.llvm.org/"
  depends=('clang-git' 'python2')
  provides=('clang-analyzer')
  conflicts=('clang-analyzer')
  replaces=('clang-analyzer')

  cd "${srcdir}/llvm/tools/clang"

  install -d "${pkgdir}"/usr/{bin,lib/clang-analyzer}
  for _tool in scan-{build,view}; do
    cp -r tools/${_tool} "${pkgdir}/usr/lib/clang-analyzer/"
    ln -s /usr/lib/clang-analyzer/${_tool}/${_tool} "${pkgdir}/usr/bin/"
  done

  ln -s /usr/bin/clang "${pkgdir}/usr/lib/clang-analyzer/scan-build/"

  install -d "${pkgdir}/usr/share/man/man1"
  mv "${pkgdir}/usr/lib/clang-analyzer/scan-build/scan-build.1" \
     "${pkgdir}/usr/share/man/man1/"

  sed -e 's|env python$|&2|' \
      -e 's|/usr/bin/python$|&2|' \
      -i "${pkgdir}/usr/lib/clang-analyzer/scan-view/scan-view" \
         "${pkgdir}/usr/lib/clang-analyzer/scan-build/set-xcode-analyzer"

  python2 -m compileall "${pkgdir}/usr/lib/clang-analyzer"
  python2 -O -m compileall "${pkgdir}/usr/lib/clang-analyzer"

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang-tools-extra-git() {
  pkgdesc="Extra tools built using Clang's tooling APIs"
  url="http://clang.llvm.org/"
  depends=("clang-git")
  provides=('clang-tools-extra')
  conflicts=('clang-tools-extra')
  replaces=('clang-tools-extra')

  cd "${srcdir}/llvm/tools/clang/tools/extra"

  make DESTDIR="$pkgdir" install

  chmod -x "$pkgdir"/usr/lib/*.a

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim: ft=sh syn=sh et

