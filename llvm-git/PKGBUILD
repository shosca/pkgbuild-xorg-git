# Maintainer: Armin K. <krejzi at email dot com>
# Contributor: Christian Babeux <christian.babeux@0x80.ca>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

_gitroot="http://llvm.org/git/llvm"
_gitname="llvm"
_pkgname=$_gitname

pkgname=(llvm-git llvm-libs-git lldb-git clang-git clang-analyzer-git)
pkgver=r121336.c21a0f3
pkgrel=1
pkgdesc='Low Level Virtual Machine'
arch=('i686' 'x86_64')
url="http://llvm.org"
license=('custom:University of Illinois')
depends=('libedit')
makedepends=('git' 'libffi' 'python2' 'python-sphinx' 'chrpath' 'swig')
options=('staticlibs' '!strip')
source=(git+$_gitroot
        git+http://llvm.org/git/clang.git
        git+http://llvm.org/git/clang-tools-extra.git
        git+http://llvm.org/git/compiler-rt.git
        git+http://llvm.org/git/lldb.git
        llvm-Config-config.h
        llvm-Config-llvm-config.h
        fix-enable-shared.patch
        D11717.diff)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '312574e655f9a87784ca416949c505c452b819fad3061f2cde8aced6540a19a3'
            '597dc5968c695bbdbb0eac9e8eb5117fcd2773bc91edf5ec103ecffffab8bc48'
            '7d0b549cff584bee55bd15d774ad6035efbf8b504a19a2c660dbbcc8ef14c7a6'
            '494ee97caa5594d99331ff753818e26c1feef5eedc89581af1585f39acac553d')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  rm -rf tools/clang
  git clone ${srcdir}/clang tools/clang

  rm -rf tools/clang/tools/extra
  git clone ${srcdir}/clang-tools-extra tools/clang/tools/extra

  rm -rf projects/compiler-rt
  git clone ${srcdir}/compiler-rt projects/compiler-rt

  rm -rf tools/lldb
  git clone ${srcdir}/lldb tools/lldb


  sed -i 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
    Makefile.config.in

  # Fix definition of LLVM_CMAKE_DIR in LLVMConfig.cmake
  sed -i '/@LLVM_CONFIG_CMAKE_DIR@/s:$(PROJ_cmake):$(PROJ_prefix)/share/llvm/cmake:' \
    cmake/modules/Makefile

  # Use Python 2
  find tools/lldb -name Makefile -exec sed -i 's/python-config/python2-config/' {} +
  sed -i 's|/usr/bin/env python|&2|' \
    tools/lldb/scripts/Python/{build-swig-Python,finish-swig-Python-LLDB}.sh

  rm -rf "${srcdir}/build"
  mkdir -p "${srcdir}/build"
}

build() {
  cd "${srcdir}/build"

  ${srcdir}/${_gitname}/configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --enable-libffi \
      --enable-targets=all \
      --disable-expensive-checks \
      --disable-debug-runtime \
      --disable-assertions \
      --with-binutils-include=/usr/include \
      --with-python=/usr/bin/python2 \
      --enable-optimized

  make REQUIRES_RTTI=1

  # remove reference to srcdir
  sed -e "s:\$(LLVM_SRC_ROOT):/usr/src/llvm:" \
      -e "s:\$(LLVM_OBJ_ROOT):/usr/src/llvm:" \
      -i tools/llvm-config/Makefile

  make REQUIRES_RTTI=1
  make -C ${srcdir}/${_gitname}/docs -f Makefile.sphinx man
  make -C ${srcdir}/${_gitname}/docs -f Makefile.sphinx html
  make -C ${srcdir}/${_gitname}/tools/clang/docs -f Makefile.sphinx html
}

package_llvm-git() {
  pkgdesc="Low Level Virtual Machine"
  depends=('llvm-libs-git' 'perl')
  provides=('llvm')
  conflicts=('llvm')
  replaces=('llvm')

  make -C "${srcdir}/build" DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/usr/bin/{c-index-test,clang*}
  rm -rf ${pkgdir}/usr/include/clang*
  rm -rf ${pkgdir}/usr/include/lldb*
  rm -rf ${pkgdir}/usr/lib/clang*
  rm -rf ${pkgdir}/usr/lib/lldb*
  rm -rf ${pkgdir}/usr/lib/libclang*
  rm -rf ${pkgdir}/usr/lib/liblldb*
  rm -rf ${pkgdir}/usr/share/clang*
  rm -rf ${pkgdir}/usr/share/doc/clang*
  rm -rf ${pkgdir}/usr/share/man/man1/clang*
  rm -rf ${pkgdir}/usr/lib/python*/site-packages/clang
  rm -rf ${pkgdir}/usr/lib/python*/site-packages/lldb

  rm -rf "${srcdir}"/*.so
  mv "${pkgdir}"/usr/lib/libLLVM-*.so "${srcdir}"

  rm -rf "${pkgdir}"/usr/lib/*LLVMHello.*

  chmod -x "${pkgdir}"/usr/lib/*.a

  # Remove reference to pkgdir
  sed -e "s:${pkgdir}::g" -i "${pkgdir}/usr/share/llvm/cmake/LLVMConfig.cmake"

  # Symlink LLVMgold.so into /usr/lib/bfd-plugins
  # (https://bugs.archlinux.org/task/28479)
  install -d "${pkgdir}/usr/lib/bfd-plugins"
  ln -s ../LLVMgold.so "${pkgdir}/usr/lib/bfd-plugins/LLVMgold.so"

  if [[ $CARCH == x86_64 ]]; then
    # Needed for multilib (https://bugs.archlinux.org/task/29951)
    # Header stubs are taken from Fedora
    for _header in config llvm-config; do
      mv "${pkgdir}/usr/include/llvm/Config/${_header}"{,-64}.h
      cp "${srcdir}/llvm-Config-${_header}.h" \
           "${pkgdir}/usr/include/llvm/Config/${_header}.h"
    done
  fi

  install -d "${pkgdir}/usr/share/man/man1"
  cp ${srcdir}/${_gitname}/docs/_build/man/*.1 "${pkgdir}/usr/share/man/man1/"

  cp -r ${srcdir}/${_gitname}/docs/_build/html/* "${pkgdir}/usr/share/doc/$_pkgname/html/"
  rm -r "${pkgdir}/usr/share/doc/${_pkgname}/html/_sources"

  install -Dm644 ${srcdir}/${_gitname}/LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_llvm-libs-git() {
  pkgdesc="Low Level Virtual Machine (runtime library)"
  depends=('gcc-libs' 'zlib' 'libedit' 'libffi')
  provides=('llvm-libs')
  conflicts=('llvm-libs')
  replaces=('llvm-libs')

  mkdir -p "${pkgdir}"/usr/lib/

  mv "${srcdir}"/libLLVM-*.so "${pkgdir}/usr/lib/"

  for _shlib in "${pkgdir}"/usr/lib/*.so
  do
    test -L "${_shlib}" && ln -sf $(basename $(readlink ${_shlib})) ${_shlib}
  done

  install -Dm644 "${srcdir}/${_pkgname}/LICENSE.TXT" \
                 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lldb-git() {
  pkgdesc="Next generation, high-performance debugger"
  url="http://lldb.llvm.org/"
  depends=("llvm-libs=$pkgver-$pkgrel" 'libedit' 'python2')

  cd "$srcdir/build/tools/lldb"

  make DESTDIR="$pkgdir" install

  # Fix permissions of static libs
  chmod -x "$pkgdir"/usr/lib/*.a

  # Remove insecure rpath
  chrpath -d "$pkgdir"/usr/bin/*

  # Compile Python scripts
  python2 -m compileall "$pkgdir/usr/lib/python2.7/site-packages/lldb"
  python2 -O -m compileall "$pkgdir/usr/lib/python2.7/site-packages/lldb"

  install -Dm644 ${srcdir}/${_gitname}/tools/lldb/LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang-git() {
  pkgdesc="C language family frontend for LLVM"
  url="http://clang.llvm.org/"
  depends=('llvm-git' 'gcc')
  provides=('clang' 'clang-tools-extra' 'clang-tools-extra-git')
  conflicts=('clang' 'clang-tools-extra' 'clang-tools-extra-git')
  replaces=('clang' 'clang-tools-extra' 'clang-tools-extra-git')

  sed -e 's:$(PROJ_prefix)/share/doc/llvm:$(PROJ_prefix)/share/doc/clang:' \
    -i "${srcdir}/build/Makefile.config"

  make -C "${srcdir}/build/tools/clang" DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}/usr/bin/pp-trace"
  rm -r "${pkgdir}/usr/lib/libmodernizeCore.a"
  find "${pkgdir}/usr/lib/" -iname "libLTO*" -exec rm  {} \;

  chmod -x "${pkgdir}"/usr/lib/*.a

  sed -e 's:$(PROJ_prefix)/share/doc/clang:$(PROJ_prefix)/share/doc/llvm:' \
      -i "${srcdir}/build/Makefile.config"

  cp -r ${srcdir}/${_gitname}/docs/_build/html/* "${pkgdir}/usr/share/doc/clang/html/"
  rm -r "${pkgdir}/usr/share/doc/clang/html/_sources"

  install -d "${pkgdir}/usr/lib/python2.7/site-packages"
  cp -r ${srcdir}/${_gitname}/tools/clang/bindings/python/clang "$pkgdir/usr/lib/python2.7/site-packages/"
  python2 -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/clang"
  python2 -O -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/clang"


  install -Dm644 ${srcdir}/${_gitname}/tools/clang/LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang-analyzer-git() {
  pkgdesc="A source code analysis framework"
  url="http://clang-analyzer.llvm.org/"
  depends=('clang-git' 'python2')
  provides=('clang-analyzer')
  conflicts=('clang-analyzer')
  replaces=('clang-analyzer')

  cd "${srcdir}/${_gitname}/tools/clang"

  install -d ${pkgdir}/usr/{bin,lib/clang-analyzer}
  cp -r tools/scan-build "${pkgdir}/usr/lib/clang-analyzer/"
  ln -s /usr/lib/clang-analyzer/scan-build/scan-build "${pkgdir}/usr/bin/"

  cp -r tools/scan-view "${pkgdir}/usr/lib/clang-analyzer/"
  ln -s /usr/lib/clang-analyzer/scan-view/scan-view "${pkgdir}/usr/bin/"

  ln -s /usr/bin/clang "${pkgdir}/usr/lib/clang-analyzer/scan-build/"

  install -d "${pkgdir}/usr/share/man/man1"
  mv "${pkgdir}/usr/lib/clang-analyzer/scan-build/scan-build.1" \
     "${pkgdir}/usr/share/man/man1/"

  sed -e 's|env python$|&2|' \
      -e 's|/usr/bin/python$|&2|' \
      -i "${pkgdir}/usr/lib/clang-analyzer/scan-view/scan-view" \
         "${pkgdir}/usr/lib/clang-analyzer/scan-build/set-xcode-analyzer"

  python2 -m compileall "${pkgdir}/usr/lib/clang-analyzer"
  python2 -O -m compileall "${pkgdir}/usr/lib/clang-analyzer"

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ft=sh syn=sh et

