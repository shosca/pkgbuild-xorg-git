# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

_gitroot="http://llvm.org/git/llvm"
_gitname="llvm"
_realver="3.4svn"

pkgname=('lib32-llvm-git' 'lib32-llvm-libs-git')
pkgver=20131106
pkgrel=1805
arch=('x86_64')
url="http://llvm.org"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('lib32-libffi' 'llvm-git' 'python2' 'ocaml')
makedepends=('git' 'gcc-multilib')
provides=('lib32-llvm')
conflicts=('lib32-llvm')
source=(git+$_gitroot
        git+http://llvm.org/git/compiler-rt.git
)
md5sums=(SKIP SKIP)
options=('!strip')

prepare() {
  cd "${srcdir}/${_gitname}"

  rm -rf tools/clang
  rm -rf projects/compiler-rt

  git clone ${srcdir}/compiler-rt projects/compiler-rt

  # Fix installation directories, ./configure doesn't seem to set them right
  sed -i -e 's:\$(PROJ_prefix)/lib:$(PROJ_prefix)/lib32:' \
         -e 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
    Makefile.config.in
  sed -i '/ActiveLibDir = ActivePrefix/s:lib:lib32:' \
    tools/llvm-config/llvm-config.cpp
  sed -i 's:LLVM_LIBDIR="${prefix}/lib":LLVM_LIBDIR="${prefix}/lib32":' \
    autoconf/configure.ac \
    configure

  # Fix insecure rpath (http://bugs.archlinux.org/task/14017)
  sed -i 's:$(RPATH) -Wl,$(\(ToolDir\|LibDir\|ExmplDir\))::g' Makefile.rules
}


build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # Include location of libffi headers in CPPFLAGS
  CPPFLAGS+=" $(pkg-config --cflags libffi)"

  ./configure \
    --host=i386-pc-linux-gnu \
    --target=i386-pc-linux-gnu \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=all \
    --enable-experimental-targets=R600 \
    --disable-expensive-checks \
    --disable-debug-runtime \
    --disable-assertions \
    --with-binutils-include=/usr/include \
    --with-python=/usr/bin/python2 \
    --enable-optimized

  make REQUIRES_RTTI=1 ENABLE_OPTIMIZED=1
}

package_lib32-llvm-git() {
  pkgdesc="Low Level Virtual Machine (32-bit)"
  depends=('lib32-llvm-libs-git' 'llvm')
  provides=('lib32-llvm')
  conflicts=('lib32-llvm')

  cd "${srcdir}/${_gitname}"
  make -j1 DESTDIR="$pkgdir" install

  # The runtime library goes into lib32-llvm-libs
  mv "$pkgdir/usr/lib32/libLLVM-$_realver.so" "$srcdir"

  # Fix permissions of static libs
  chmod -x "$pkgdir"/usr/lib32/*.a

  mv "$pkgdir/usr/bin/i386-pc-linux-gnu-llvm-config" "$pkgdir/usr/lib32/llvm-config"

  # Get rid of example Hello transformation
  rm "$pkgdir"/usr/lib32/*LLVMHello.*

  # Symlink LLVMgold.so into /usr/lib/bfd-plugins
  # (https://bugs.archlinux.org/task/28479)
  install -d "$pkgdir/usr/lib32/bfd-plugins"
  ln -s ../LLVMgold.so "$pkgdir/usr/lib32/bfd-plugins/LLVMgold.so"

  mv "$pkgdir"/usr/include/llvm/Config/*config.h "$pkgdir/"
  rm -rf "$pkgdir"/usr/{bin,include,lib,share/{doc,man}}

  install -d "$pkgdir/usr/include/llvm/Config"
  mv "$pkgdir/config.h" "$pkgdir/usr/include/llvm/Config/config-32.h"
  mv "$pkgdir/llvm-config.h" "$pkgdir/usr/include/llvm/Config/llvm-config-32.h"

  install -d "$pkgdir"/usr/bin
  mv "$pkgdir/usr/lib32/llvm-config" "$pkgdir/usr/bin/llvm-config32"

  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lib32-llvm-libs-git() {
  pkgdesc="Low Level Virtual Machine (runtime library)(32-bit)"
  depends=('lib32-libffi' 'lib32-zlib' 'lib32-gcc-libs')
  provides=('lib32-llvm-libs')
  conflicts=('lib32-llvm-libs')

  cd "${srcdir}"

  install -D libLLVM-$_realver.so "$pkgdir/usr/lib32/libLLVM-$_realver.so"

  install -Dm644 ${_gitname}/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim: ft=sh syn=sh et

