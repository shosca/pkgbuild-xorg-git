# $Id: PKGBUILD 152793 2012-03-09 16:30:31Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXtst"
_gitname="libXtst"

pkgname=libxtst-git
pkgver=r112.48d2656
pkgrel=48
pkgdesc="X11 Testing -- Resource extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxext-git' 'libxi-git' 'recordproto-git' 'inputproto-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('libxtst')
conflicts=('libxtst')
replaces=('libxtst')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
