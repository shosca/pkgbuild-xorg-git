# $Id: PKGBUILD 151743 2012-03-01 23:01:51Z bisson $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/font/util"
_gitname="util"

pkgname=xorg-font-util-git
pkgver=r75.9f53de0
pkgrel=1
pkgdesc='X.Org font utilities'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
makedepends=('xorg-util-macros-git' 'git')
replaces=('font-util')
provides=('font-util' 'xorg-font-util')
conflicts=('font-util' 'xorg-font-util')
groups=('xorg-fonts' 'xorg')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --with-mapdir=/usr/share/fonts/util \
      --with-fontrootdir=/usr/share/fonts \

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

