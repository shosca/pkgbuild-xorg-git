# $Id: PKGBUILD 202383 2013-12-21 14:15:26Z bpiotrowski $
# Maintainer : Ionut Biru <ibiru@archlinux.org>

_realver=1.2.3.pre1
_gitroot="git://anongit.freedesktop.org/vaapi/intel-driver"
_gitname="intel-driver"

pkgname=libva-intel-driver-git
pkgver=r1207.b48ba79
pkgrel=3
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libva-git')
makedepends=('git')
provides=('libva-intel-driver=${_realver}')
conflicts=('libva-intel-driver')
replaces=('libva-intel-driver' 'libva-driver-intel')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  install -m644 -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
