# $Id: PKGBUILD 134284 2011-08-02 18:56:04Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xset"
_gitname="xset"

pkgname=xorg-xset-git
pkgver=r109.217c8ad
pkgrel=48
pkgdesc="User preference utility for X"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxmu-git')
makedepends=('xorg-util-macros-git' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xset')
conflicts=('xorg-xset')
replaces=('xorg-xset')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --without-fontcache --without-xf86misc
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
