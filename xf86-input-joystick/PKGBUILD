# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-joystick
_gitname=repo-xf86-input-joystick

pkgname=xf86-input-joystick
pkgver=1.6.3.0.r284.gde42486
pkgrel=1
pkgdesc="X.Org Joystick input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('git' 'xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.1' 'resourceproto' 'scrnsaverproto')
conflicts=('X-ABI-XINPUT_VERSION<23.1')
groups=('xorg-drivers' 'xorg')
source=($_gitname::$_gitroot 50-joystick.conf)
sha256sums=('SKIP'
            'd6ddaec1767e1a73f8eacc6e58b0fd1d1ca5e73bbf58b6a106ebf92589b87483')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr \
    --disable-selective-werror
  make
}

package(){
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/X11/xorg.conf.d"
  install -m644 "${srcdir}/50-joystick.conf" "${pkgdir}/usr/share/X11/xorg.conf.d/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
