# Maintainer: Christoph Haag <haagch at studi.informatik.uni-stuttgart.de>

_gitroot="git://cgit.freedesktop.org/mesa/glu/"
_gitname="repo-glu"

pkgname=glu
pkgver=9.0.198.0692115
pkgrel=7
pkgdesc="GL utility library for mesa builds from git after 2012-08-31"
arch=('i686' 'x86_64')
url="http://mesa.freedesktop.org/"
license=('LGPL')
depends=('mesa-libgl' 'gcc-libs')
makedepends=('git' 'libedit')
options=(!libtool)
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s" \
    "$(git describe --long | cut -d "-" -f2 | cut -d'.' -f1-2 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et

