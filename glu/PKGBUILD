# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Serkan Hosca <serkan@hosca.com>

_gitroot="git://cgit.freedesktop.org/mesa/glu/"
_gitname="repo-glu"

pkgname=glu
pkgver=9.0.0.3.r199.g0bf42e4
pkgrel=9
pkgdesc="Mesa OpenGL Utility library"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/mesa/glu/"
license=('LGPL')
depends=('libgl')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et
