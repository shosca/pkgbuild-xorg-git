# $Id: PKGBUILD 154156 2012-03-23 20:01:59Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_realver=1.2.4
_gitroot='git://anongit.freedesktop.org/xorg/app/xkbcomp'
_gitname='xkbcomp'

pkgname=xorg-xkbcomp-git
pkgver=r153.d4e02a0
pkgrel=1
pkgdesc="X Keyboard description compiler"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile-git')
makedepends=('xorg-util-macros-git' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xkbcomp=1.2.4')
conflicts=('xorg-xkbcomp')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

