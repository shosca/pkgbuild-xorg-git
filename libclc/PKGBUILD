# Maintainer: Armin K. <krejzi at email dot com>

_gitroot='http://llvm.org/git/libclc.git'
_gitname=repo-libclc

pkgname=libclc
pkgver=0.2.0.r297.gd5dbee6
pkgrel=3
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('i686' 'x86_64')
url="http://libclc.llvm.org/"
license=('MIT')
makedepends=('clang' 'git' 'python2')
source=($_gitname::git+$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.py | grep -e '^version_major' | tr -d ';' | cut -d' ' -f 3)" \
    "$(git show HEAD:configure.py | grep -e '^version_minor' | tr -d ';' | cut -d' ' -f 3)" \
    "$(git show HEAD:configure.py | grep -e '^version_patch' | tr -d ';' | cut -d' ' -f 3)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  /usr/bin/python2 ./configure.py \
    --prefix=/usr \
    --with-llvm-config=/usr/bin/llvm-config
  sed -i 's/ python / python2 /' Makefile
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

