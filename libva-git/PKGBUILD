# $Id: PKGBUILD 198256 2013-10-30 13:45:51Z allan $
# Maintainer : Ionut Biru <ibiru@archlinux.org>

_gitroot="git://anongit.freedesktop.org/vaapi/libva"
_gitname="libva"

pkgname=libva-git
pkgver=r1099.70b80c0
pkgrel=2
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('mesa-libgl-git' 'libdrm-git' 'libxfixes-git')
makedepends=('gcc-multilib' 'git' 'mesa-libgl-git')
optdepends=('libva-vdpau-driver-git: vdpau back-end for nvidia'
            'libva-intel-driver-git: back-end for intel cards')
provides=('libva')
conflicts=('libva')
replaces=('libva')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  install -m644 -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
