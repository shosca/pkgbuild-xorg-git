# $Id: PKGBUILD 152301 2012-03-06 15:00:25Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXmu"
_gitname="libXmu"

pkgname=libxmu-git
pkgver=r118.22d9c59
pkgrel=24
pkgdesc="X11 miscellaneous micro-utility library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('libxext-git' 'libxt-git')
makedepends=('xorg-util-macros-git' 'git')
license=('custom')
provides=('libxmu')
conflicts=('libxmu')
replaces=('libxmu')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

