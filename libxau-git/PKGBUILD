# $Id: PKGBUILD 152650 2012-03-07 19:44:52Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXau"
_gitname="libXau"

pkgname=libxau-git
pkgver=r100.1e4635b
pkgrel=11
pkgdesc="X11 authorisation library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'xproto-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'git')
license=('custom')
provides=('libxau')
conflicts=('libxau')
replaces=('libxau')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

