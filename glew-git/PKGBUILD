# $Id: PKGBUILD 192292 2013-08-09 20:35:39Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: SleepyDog

_realver=1.10.0
_gitroot="git://github.com/nigels-com/glew"
_gitname="glew"

pkgname=glew-git
pkgver=r763.0819658
pkgrel=87
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('i686' 'x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu-git' 'libxi-git' 'glu-git')
makedepends=('git' 'wget' 'perl' 'make')
source=($_gitroot)
sha256sums=('SKIP')
provides=('glew=${_realver}')
replaces=('glew')
conflicts=('glew')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  make -j1 extensions
  sed -i 's|lib64|lib|' config/Makefile.linux
  sed -i '/^.PHONY: .*\.pc$/d' Makefile
  make -j1
}

package() {
  cd "${srcdir}/${_gitname}"
  make GLEW_DEST="${pkgdir}/usr" install.all
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm "${pkgdir}"/usr/lib/{libGLEW,libGLEWmx}.a
  chmod 0755 "${pkgdir}"/usr/lib/libGLEW*.so.*
}
