# Maintainer: aboe76 AT gmail DOT com>
# contribution: EdwardXXIV

_gitroot="git://anongit.freedesktop.org/git/pixman"
_gitname="repo-pixman"

pkgname=lib32-pixman
pkgver=0.35.1.7.r2324.g7c6066b
pkgrel=2
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('x86_64')
url="http://pixman.org/"
license=('GPL')
depends=('lib32-pango' 'glibc')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'pkgconfig')
options=('!libtool')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep "m4_define(\[pixman_major\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep "m4_define(\[pixman_minor\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep "m4_define(\[pixman_micro\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-gtk \
    --disable-static

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}/" install

  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ft=sh syn=sh et
