# $Id: PKGBUILD 356 2008-04-18 22:56:27Z aaron $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libpciaccess"
_gitname="libpciaccess"

pkgname=lib32-libpciaccess-git
pkgver=r273.b9c0688
pkgrel=1
pkgdesc="X11 PCI access library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-glibc' 'libpciaccess-git')
makedepends=('gcc-multilib' 'git' 'pkgconfig' 'xorg-util-macros-git')
provides=('lib32-libpciaccess')
conflicts=('lib32-libpciaccess')
options=('!libtool')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

