# $Id: PKGBUILD 175089 2013-01-13 09:51:21Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xmessage"
_gitname="xmessage"

pkgname=xorg-xmessage-git
pkgver=r67.2ded896
pkgrel=44
pkgdesc="Display a message or query in a window"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('xorg-xmessage')
conflicts=('xorg-xmessage')
replaces=('xorg-xmessage')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
