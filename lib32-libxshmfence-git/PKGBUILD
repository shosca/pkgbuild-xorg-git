# $Id: PKGBUILD 158868 2012-05-12 16:25:29Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libxshmfence"
_gitname="libxshmfence"

pkgname=lib32-libxshmfence-git
pkgver=r24.9c4f070
pkgrel=2
pkgdesc="X11 shared memory fences using futexes"
arch=('x86_64')
url="http://xorg.freedesktop.org"
depends=('libxshmfence-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'git')
options=(!libtool)
license=('custom')
provides=('lib32-libxshmfence')
conflicts=('lib32-libxshmfence')
replaces=('lib32-libxshmfence')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

