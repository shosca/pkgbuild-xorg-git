# $Id: PKGBUILD 174025 2013-01-01 19:16:09Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/libxcb"
_gitname="repo-libxcb"

pkgname=lib32-libxcb
pkgver=1.11.65.r652.g095353f
pkgrel=2
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto' 'libxcb' 'lib32-libxdmcp' 'lib32-libxau')
makedepends=('gcc-multilib' 'pkgconfig' 'libxslt' 'python2' 'xorg-util-macros' 'git')
options=('!libtool')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --tags | cut -d "-" -f1-2 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --tags | cut -d "-" -f3)"
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -e 's/pthread-stubs //g' -i configure.ac

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export PYTHON=/usr/bin/python2 

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

