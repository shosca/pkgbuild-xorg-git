# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/libxcb"
_gitname="repo-libxcb"

pkgname=lib32-libxcb
_pkgbasename=${pkgname:6}
pkgver=1.12.3.r657.g65b298c
pkgrel=4
pkgdesc="X11 client-side library (32-bit)"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('lib32-libxdmcp' 'lib32-libxau' $_pkgbasename)
makedepends=('gcc-multilib' 'pkgconfig' 'libxslt' 'python2' 'xorg-util-macros' 'git')
makedepends=('git' 'libxslt' 'python2' 'xorg-util-macros' 'gcc-multilib'
             'autoconf')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -e 's/pthread-stubs //g' -i configure.ac

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ft=sh syn=sh et
