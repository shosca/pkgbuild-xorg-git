# $Id: PKGBUILD 192292 2013-08-09 20:35:39Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: SleepyDog

_gitroot="git://github.com/nigels-com/glew"
_gitname="repo-glew"

pkgname=glew
pkgver=2.0.0.79.r954.gc4bade8
pkgrel=9
pkgdesc="The OpenGL Extension Wrangler Library"
arch=('i686' 'x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('libxmu' 'libxi' 'glu')
makedepends=('git' 'wget' 'perl' 'python' 'python2' 'make')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:config/version | grep 'GLEW_MAJOR = ' | cut -d' ' -f3)" \
    "$(git show HEAD:config/version | grep 'GLEW_MINOR = ' | cut -d' ' -f3)" \
    "$(git show HEAD:config/version | grep 'GLEW_MICRO = ' | cut -d' ' -f3)" \
    "$(git rev-list  $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  find -type f -exec sed -i 's/python/python2/g' {} +
  make -j1 extensions

  sed -i 's|lib64|lib|' config/Makefile.linux
  sed -i '/^.PHONY: .*\.pc$/d' Makefile
  make -j1
}

package() {
  cd "${srcdir}/${_gitname}"
  make GLEW_DEST="${pkgdir}/usr" install.all
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm -f "${pkgdir}"/usr/lib/{libGLEW,libGLEWmx}.a
  chmod 0755 "${pkgdir}"/usr/lib/libGLEW*.so.*
}
