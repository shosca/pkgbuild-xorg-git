# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_gitroot="git://anongit.freedesktop.org/wayland/wayland"
_gitname="repo-wayland"

pkgname=lib32-wayland
_pkgbasename=${pkgname:6}
pkgver=1.11.93.0.r1943.gda58173
pkgrel=1
pkgdesc='A computer display server protocol (32 bit)'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-glibc' 'lib32-libffi' 'lib32-expat' 'lib32-libxml2' 'wayland')
makedepends=('git' 'gcc-multilib')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_major' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_minor' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_micro' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --libdir=/usr/lib32 \
    --disable-documentation
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
