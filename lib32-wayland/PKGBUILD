# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_gitroot="git://anongit.freedesktop.org/wayland/wayland"
_gitname="repo-wayland"

pkgname=lib32-wayland
pkgver=1.10.90.11.r1859.g4a41d26
pkgrel=1
pkgdesc='A computer display server protocol (32 bit)'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-libffi' 'lib32-libxml2' 'wayland')
makedepends=('git' 'gcc-multilib' 'lib32-expat')
options=(!libtool)
conflicts=('lib32-libwayland')
replaces=('lib32-libwayland')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_major' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_minor' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_micro' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --libdir=/usr/lib32 \
    --disable-documentation
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
