# Maintainer: Joel Teichroeb <joel@teichroeb.net>

_gitroot="git://anongit.freedesktop.org/wayland/wayland"
_gitname="repo-wayland"

pkgname=wayland
pkgver=1.9.93.1.r1847.gbf34ac7
pkgrel=5
pkgdesc="A modern display server."
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org"
license=('MIT')
depends=('libffi' 'libxml2' 'wayland-protocols')
makedepends=('git' 'expat' 'doxygen')
conflicts=('libwayland')
replaces=('libwayland')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f1-2 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f3)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr \
    --disable-static \
    --disable-documentation
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
