# Maintainer: Joel Teichroeb <joel@teichroeb.net>

_gitroot="git://anongit.freedesktop.org/wayland/wayland"
_gitname="repo-wayland"

pkgname=wayland
pkgver=1.10.90.10.r1858.g5646236
pkgrel=1
pkgdesc="A modern display server."
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org"
license=('MIT')
depends=('libffi' 'libxml2' 'wayland-protocols')
makedepends=('git' 'expat' 'doxygen')
conflicts=('libwayland')
replaces=('libwayland')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_major' | tr '[]{}(),' ' ' | tr -s ' ' | cut -d ' ' -f3)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_minor' | tr '[]{}(),' ' ' | tr -s ' ' | cut -d ' ' -f3)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[wayland_micro' | tr '[]{}(),' ' ' | tr -s ' ' | cut -d ' ' -f3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr \
    --disable-static \
    --disable-documentation
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
