# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>

_gitroot='git://anongit.freedesktop.org/libxklavier'
_gitname='repo-libxklavier'

pkgname=libxklavier
pkgver=5.4.2.r339.g65c132a
pkgrel=46
pkgdesc="High-level API for X Keyboard Extension"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2' 'libxkbfile' 'libxml2' 'xkeyboard-config' 'libxi' 'iso-codes')
makedepends=('git' 'gobject-introspection' 'vala' 'intltool' 'gtk-doc' 'libxfixes')
options=('!emptydirs')
url="http://gswitchit.sourceforge.net"
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --with-xkb-bin-base=/usr/bin \
    --with-xkb-base=/usr/share/X11/xkb --disable-static --enable-gtk-doc \
    --enable-vala --enable-introspection
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
