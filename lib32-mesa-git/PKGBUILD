# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

pkgbase=lib32-mesa-git
pkgname=('lib32-mesa-dri-git' 'lib32-mesa-vdpau-git' 'lib32-mesa-git' 'lib32-mesa-libgl-git')
pkgver=r67072.a9e7789
pkgrel=1
arch=('x86_64')
makedepends=('git' 'python2' 'lib32-libxml2' 'lib32-expat' 'lib32-libx11-git'
             'glproto-git' 'lib32-libdrm-git' 'dri2proto-git' 'dri3proto-git'
             'presentproto-git' 'lib32-libxshmfence-git' 'lib32-libxxf86vm-git'
             'lib32-libxdamage-git' 'gcc-multilib' 'lib32-elfutils'
             'lib32-llvm-git' 'lib32-systemd' 'lib32-libvdpau-git'
             'lib32-libxvmc-git' 'lib32-wayland')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=($_gitroot LICENSE)
sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')
pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG=/usr/bin/llvm-config32

  ./autogen.sh \
    --build=i686-pc-linux-gnu --host=i686-pc-linux-gnu \
    --target=i686-pc-linux-gnu \
    --libdir=/usr/lib32 \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-egl-platforms=drm,x11,wayland \
    --enable-llvm-shared-libs \
    --enable-egl \
    --disable-gallium-egl \
    --disable-gallium-gbm \
    --enable-gbm \
    --enable-gallium-llvm \
    --enable-shared-glapi \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-vdpau

  make

  rm -rf $srcdir/fakeinstall
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_lib32-mesa-dri-git() {
  pkgdesc="Mesa DRI drivers (32-bit)"
  depends=('lib32-expat' 'lib32-libdrm-git' 'lib32-libtxc_dxtn')
  conflicts=('lib32-mesa-dri' 'lib32-ati-dri' 'lib32-ati-dri-git' 'lib32-intel-dri' 'lib32-intel-dri-git' 'lib32-nouveau-dri' 'lib32-nouveau-dri-git')
  provides=('lib32-mesa-dri' 'lib32-ati-dri' 'lib32-ati-dri-git' 'lib32-intel-dri' 'lib32-intel-dri-git' 'lib32-nouveau-dri' 'lib32-nouveau-dri-git')
  replaces=('lib32-mesa-dri' 'lib32-ati-dri' 'lib32-ati-dri-git' 'lib32-intel-dri' 'lib32-intel-dri-git' 'lib32-nouveau-dri' 'lib32-nouveau-dri-git')

  install -m755 -d ${pkgdir}/usr/lib32/dri
  # ati-dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{r200,r300,r600,radeon{,si}}_dri.so ${pkgdir}/usr/lib32/dri
  # nouveau-dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/nouveau{,_vieux}_dri.so ${pkgdir}/usr/lib32//dri
  # intel-dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib32/dri
  # swrast
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{kms_,}swrast_dri.so ${pkgdir}/usr/lib32/dri

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-vdpau-git() {
  pkgdesc="Mesa VDPAU drivers (32-bit)"
  depends=('lib32-libdrm-git' 'lib32-libx11-git' 'lib32-expat' 'lib32-elfutils')
  provides=('lib32-mesa-vdpau')
  conflicts=('lib32-mesa-vdpau')
  replaces=('lib32-mesa-vdpau')

  install -m755 -d ${pkgdir}/usr/lib32
  mv -v ${srcdir}/fakeinstall/usr/lib32/vdpau ${pkgdir}/usr/lib32

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-git() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('lib32-libdrm-git' 'lib32-libxxf86vm-git' 'lib32-libxdamage-git'
           'lib32-libxshmfence-git' 'lib32-systemd' 'lib32-elfutils'
           'lib32-llvm-libs-git' 'lib32-wayland-git' 'lib32-mesa-dri-git'
           'mesa-git')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('lib32-mesa' 'lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  conflicts=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  replaces=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')

  install -m755 -d ${pkgdir}/usr/lib32
  mv -v ${srcdir}/fakeinstall/usr/lib32/lib{OSMesa,gbm,glapi,wayland-egl}.so* ${pkgdir}/usr/lib32/
  #mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_swrast* ${pkgdir}/usr/lib32/gallium-pipe/
  # FS#41337
  #mv -v ${srcdir}/fakeinstall/usr/lib32/gbm/gbm_gallium_drm* ${pkgdir}/usr/lib32/gbm/

  mv -v ${srcdir}/fakeinstall/usr/lib32/pkgconfig ${pkgdir}/usr/lib32/

  install -m755 -d ${pkgdir}/usr/lib32/mesa
  # move libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv -v ${srcdir}/fakeinstall/usr/lib32/libGL.so*    ${pkgdir}/usr/lib32/mesa/
  mv -v ${srcdir}/fakeinstall/usr/lib32/libEGL.so*   ${pkgdir}/usr/lib32/mesa/
  mv -v ${srcdir}/fakeinstall/usr/lib32/libGLES*.so* ${pkgdir}/usr/lib32/mesa/

  # keep symlinks until pacman 4.2 FS#42046
  #install -m755 -d "${pkgdir}/usr/share/licenses/lib32-mesa"
  #install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-mesa/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-libgl-git(){
  pkgdesc="Mesa 3-D graphics library"
  depends=("lib32-mesa-git")
  provides=("lib32-libgl")
  replaces=('lib32-libgl')

  install -m755 -d ${pkgdir}/usr/lib32

  ln -s /usr/lib32/mesa/libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1.2.0
  ln -s libGL.so.1.2.0                 ${pkgdir}/usr/lib32/libGL.so.1
  ln -s libGL.so.1.2.0                 ${pkgdir}/usr/lib32/libGL.so

  ln -s /usr/lib32/mesa/libEGL.so.1.0.0 ${pkgdir}/usr/lib32/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0                 ${pkgdir}/usr/lib32/libEGL.so.1
  ln -s libEGL.so.1.0.0                 ${pkgdir}/usr/lib32/libEGL.so

  ln -s /usr/lib32/mesa/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib32/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0                 ${pkgdir}/usr/lib32/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0                 ${pkgdir}/usr/lib32/libGLESv1_CM.so

  ln -s /usr/lib32/mesa/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib32/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0                 ${pkgdir}/usr/lib32/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0                 ${pkgdir}/usr/lib32/libGLESv2.so

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

