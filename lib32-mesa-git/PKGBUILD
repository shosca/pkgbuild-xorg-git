# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_realver=10.0.0-devel
_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

pkgbase=lib32-mesa-git
pkgname=('lib32-ati-dri-git' 'lib32-intel-dri-git' 'lib32-nouveau-dri-git' 'lib32-mesa-git' 'lib32-mesa-libgl-git')
pkgver=20131101
pkgrel=1042
arch=('x86_64')
makedepends=('glproto' 'lib32-libdrm' 'lib32-libxvmc' 'lib32-libvdpau' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-expat' 'lib32-libx11' 'lib32-libxt'
             'gcc-multilib' 'dri2proto-git' 'python2' 'libxml2' 'lib32-llvm-git' 'lib32-systemd')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=($_gitroot LICENSE llvm-build-fix.patch)
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a'
         '1e6dcac650ba20b4d535971a2722b5bf')

build() {
  cd "${srcdir}/${_gitname}"

  patch -Np1 -i ../llvm-build-fix.patch

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG=/usr/bin/llvm-config32

    #--host=i686-pc-linux-gnu \
    #--target=i686-pc-linux-gnu \
    #--build=x86_64-pc-linux-gnu \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-32-bit \
    --libdir=/usr/lib32 \
    --with-llvm-shared-libs \
    --enable-egl \
    --enable-gallium-egl \
    --enable-gallium-llvm \
    --with-egl-platforms=drm,x11,wayland \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-r600-llvm-compiler

  make

  rm -rf $srcdir/fakeinstall
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_lib32-ati-dri-git() {
  depends=("lib32-mesa-libgl-git")
  pkgdesc="Mesa drivers for AMD/ATI Radeon (32-bit)"
  provides=('lib32-ati-dri=${_realver}')
  conflicts=('lib32-ati-dri' 'xf86-video-ati<6.9.0-6')

  install -m755 -d ${pkgdir}/usr/lib32/dri
  install -m755 -d ${pkgdir}/usr/lib32/gallium-pipe

  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib32/dri/
  mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_{r300,r600,radeonsi}* ${pkgdir}/usr/lib32/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-intel-dri-git() {
  pkgdesc="Mesa drivers for Intel (32-bit)"
  depends=("lib32-mesa-libgl-git")
  provides=('lib32-intel-dri=${_realver}')
  conflicts=('lib32-intel-dri')
  replaces=('lib32-intel-dri')

  install -m755 -d ${pkgdir}/usr/lib32/dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib32/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-nouveau-dri-git() {
  pkgdesc="Mesa drivers for Nouveau (32-bit)"
  depends=("lib32-mesa-libgl-git")
  provides=('lib32-nouveau-dri=${_realver}')
  conflicts=('lib32-nouveau-dri')
  replaces=('lib32-nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib32/vdpau/
  install -m755 -d ${pkgdir}/usr/lib32/dri/
  install -m755 -d ${pkgdir}/usr/lib32/gallium-pipe/

  mv -v ${srcdir}/fakeinstall/usr/lib32/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib32/dri/
  mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib32/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-git() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('lib32-libdrm' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-systemd' 'lib32-wayland')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('lib32-mesa=${_realver}' 'lib32-libglapi=${_realver}' 'lib32-osmesa=${_realver}' 'lib32-libgbm=${_realver}' 'lib32-libgles=${_realver}' 'lib32-libegl=${_realver}')
  conflicts=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  replaces=('lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv ${pkgdir}/usr/lib32/libGL.so.1.2.0   ${pkgdir}/usr/lib32/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib32/libGL.so{,.1}

  rm -rf ${pkgdir}/etc
  rm -rf ${pkgdir}/usr/include

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-libgl-git(){
  pkgdesc="Mesa 3-D graphics library"
  depends=("lib32-mesa-git")
  provides=("lib32-libgl=${_realver}")
  replaces=('lib32-libgl')

  install -m755 -d ${pkgdir}/usr/lib32

  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so
  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1
  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1.2.0

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

