# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Serkan Hosca <serkan@hosca.com>

_gitroot='git://git.freedesktop.org/git/xorg/proto/damageproto'
_gitname='repo-damageproto'

pkgname=damageproto
pkgver=1.2.1.5.r39.gd6f17be
pkgrel=4
pkgdesc="X11 Damage extension wire protocol"
arch=(any)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xproto' 'fixesproto')
makedepends=('git' 'xorg-util-macros')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
