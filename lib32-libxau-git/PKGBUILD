# $Id: PKGBUILD 152650 2012-03-07 19:44:52Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXau"
_gitname="libXau"

pkgname=lib32-libxau-git
pkgver=r100.1e4635b
pkgrel=5
pkgdesc="X11 authorisation library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'xproto-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'git')
license=('custom')
options=('!libtool')
provides=('lib32-libxau')
conflicts=('lib32-libxau')
replaces=('lib32-libxau')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

