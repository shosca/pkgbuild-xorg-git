# Maintainer: Antoine Lubineau <antoine@lubignon.info>

_gitroot='git://github.com/clbr/radeontop.git'
_gitname='repo-radeontop'

pkgname=radeontop
pkgver=0.9.2.r170.g0e82272
pkgrel=2
pkgdesc='Radeon cards monitoring utility'
arch=('i686' 'x86_64')
url='https://github.com/clbr/radeontop'
license=('GPL3')
depends=('ncurses' 'libpciaccess' 'libdrm')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f1-2 | tr - . | tr -d v)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f3)"
}

prepare() {
  cd "$srcdir/${_gitname}"
  sed -i "s/sbin/bin/" Makefile
}

build() {
  cd "$srcdir/${_gitname}"
  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR="${pkgdir}/" install
}

# vim: ft=sh syn=sh et
