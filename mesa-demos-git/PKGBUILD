# Maintainer: Perry3D <perry3d@gmail.com>

_gitroot="git://anongit.freedesktop.org/mesa/demos"
_gitname="demos"

pkgname=mesa-demos-git
pkgver=r5962.ed81452
pkgrel=37
pkgdesc="The mesa demos (glxinfo, glxgears, ...), built from the git master branch."
arch=('i686' 'x86_64')
url="http://mesa3d.org/"
license=('LGPL')
depends=('mesa-libgl-git' 'glew-git')
makedepends=('git')
provides=('mesa-demos')
replaces=('mesa-demos')
conflicts=('mesa-demos')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -i -e '/texwrap/d' src/tests/Makefile.am
  ./autogen.sh --prefix=/usr
  make -C src/xdemos
}

package() {
  cd "${srcdir}/${_gitname}"

  make -C src/xdemos DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et
