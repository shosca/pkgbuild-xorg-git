# $Id: PKGBUILD 130758 2011-07-07 22:32:22Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xwininfo"
_gitname="xwininfo"

pkgname=xorg-xwininfo-git
pkgver=r107.0c49f8f
pkgrel=24
pkgdesc="Command-line utility to print information about windows on an X server"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxcb-git')
makedepends=('xorg-util-macros-git' 'libx11' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xwininfo')
conflicts=('xorg-xwininfo')
replaces=('xorg-xwininfo')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
