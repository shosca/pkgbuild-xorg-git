# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_gitroot="git://anongit.freedesktop.org/wayland/weston"
_gitname="repo-weston"

pkgname=weston
pkgver=1.10.90.70.r5154.g94cb06a
pkgrel=7
pkgdesc="Reference implementation of a Wayland compositor"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org"
license=('MIT')
depends=('libxkbcommon' 'wayland' 'mesa' 'cairo' 'libunwind'
         'poppler-glib' 'mtdev' 'libinput' 'libxcursor' 'pixman'
         'glu' 'freerdp' 'libice')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[weston_major' \
        | sed -e 's/m4_//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[weston_minor' \
        | sed -e 's/m4_//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[weston_micro' \
        | sed -e 's/m4_//g;s/[^0-9]*//g')" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/weston \
    --enable-libinput-backend \
    --enable-xwayland \
    --enable-rdp-compositor \
    --disable-static \
    --disable-setuid-install \
    --disable-android-compositor

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
