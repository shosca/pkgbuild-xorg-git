# $Id: PKGBUILD 165113 2012-08-11 08:21:35Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_realver=1.4.2
_gitroot="git://anongit.freedesktop.org/xorg/lib/libXrandr"
_gitname="libXrandr"

pkgname=libxrandr-git
pkgver=r160.bc00b4f
pkgrel=8
pkgdesc="X11 RandR extension library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext-git' 'libxrender-git' 'randrproto-git')
makedepends=('xorg-util-macros-git' 'git')
options=('!libtool')
provides=('libxrandr=${_realver}')
conflicts=('libxrandr')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

