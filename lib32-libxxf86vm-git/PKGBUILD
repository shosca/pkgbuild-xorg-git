# $Id: PKGBUILD 152801 2012-03-09 16:38:21Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXxf86vm"
_gitname="libXxf86vm"

pkgname=lib32-libxxf86vm-git
pkgver=r86.e13937e
pkgrel=22
pkgdesc="X11 XFree86 video mode extension library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext-git' 'libxxf86vm-git')
makedepends=('xorg-util-macros-git' 'git')
options=('!libtool')
provides=('lib32-libxxf86vm')
conflicts=('lib32-libxxf86vm')
replaces=('lib32-libxxf86vm')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ft=sh syn=sh et
