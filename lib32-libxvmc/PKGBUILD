# $Id: PKGBUILD 152797 2012-03-09 16:34:16Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXvMC"
_gitname="repo-libXvMC"

pkgname=lib32-libxvmc
pkgver=1.0.9.0.r91.gab0d289
pkgrel=34
pkgdesc="X11 Video Motion Compensation extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxv' 'libxvmc')
makedepends=('pkgconfig' 'xorg-util-macros' 'git')
options=('!libtool')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.g%s" \
    "$(git describe --long | cut -d "-" -f2-3 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ft=sh syn=sh et

