# $Id: PKGBUILD 150821 2012-02-23 07:20:15Z andyrtr $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_realver=1.2.1
_gitroot="git://anongit.freedesktop.org/xorg/app/xev"
_gitname="xev"

pkgname=xorg-xev-git
pkgver=r69.3083d6f
pkgrel=8
pkgdesc='Print contents of X events'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
depends=('libx11-git' 'libxrandr-git')
makedepends=('xorg-util-macros-git' 'xproto-git' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xev=${_realver}')
conflicts=('xorg-xev')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et
