# Maintainer : josephgbr <rafael.f.f1@gmail.com>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_gitroot="git://people.freedesktop.org/~aplattner/libvdpau"
_gitname="repo-libvdpau"

pkgname=lib32-libvdpau
_pkgbasename=${pkgname:6}
pkgver=1.1.1.3.r94.ga21bf7a
pkgrel=34
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
license=('custom')
depends=('lib32-gcc-libs' $_pkgbasename)
makedepends=('gcc-multilib' 'lib32-libx11' 'git')
options=('!libtool')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/include,usr/share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
