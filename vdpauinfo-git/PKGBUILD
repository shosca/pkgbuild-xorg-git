_gitroot='git://people.freedesktop.org/~aplattner/vdpauinfo'
_gitname='vdpauinfo'

pkgname=vdpauinfo-git
pkgver=r25.178f33e
pkgrel=4
pkgdesc="CLI utility for VDPAU device querying."
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/~aplattner/vdpauinfo"
license=('custom')
depends=('libvdpau-git')
makedepends=('git')
provides=('vdpauinfo')
conflicts=('vdpauinfo')
replaces=('vdpauinfo')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

