# Maintainer: aboe76 AT gmail DOT com>
# contribution: EdwardXXIV

_gitroot="git://anongit.freedesktop.org/git/pixman"
_gitname="pixman"

pkgname=lib32-pixman-git
pkgver=r2324.7c6066b
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('x86_64')
url="http://pixman.org/"
license=('GPL')
depends=('lib32-pango' 'glibc')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'pkgconfig')
provides=('lib32-pixman')
conflicts=('lib32-pixman')
replaces=('lib32-pixman')
options=('!libtool')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-gtk \
    --disable-static

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}/" install

  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ft=sh syn=sh et
