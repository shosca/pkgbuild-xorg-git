# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://git.freedesktop.org/git/xorg/font/alias'
_gitname='repo-alias'

pkgname=xorg-fonts-alias
pkgver=1.0.3.5.r34.g50f4974
pkgrel=1
pkgdesc="X.org font alias files"
arch=('any')
url="http://xorg.freedesktop.org/"
makedepends=('git' 'xorg-util-macros' 'xorg-font-util')
license=('custom')
source=($_gitname::$_gitroot 'LICENSE')
sha256sums=('SKIP'
            '95b6410246e011be36aad0710a36a25fbfd508269cf50d17c820b29f045f08cb')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --with-fontrootdir=/usr/share/fonts
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ft=sh syn=sh et
