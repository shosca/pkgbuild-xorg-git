# $Id: PKGBUILD 165113 2012-08-11 08:21:35Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXrandr"
_gitname="repo-libXrandr"

pkgname=libxrandr
pkgver=1.5.0.0.r162.g4437436
pkgrel=38
pkgdesc="X11 RandR extension library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext' 'libxrender' 'randrproto')
makedepends=('xorg-util-macros' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2-3 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

