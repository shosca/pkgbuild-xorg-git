# Maintainer: Joel Teichroeb <joel@teichroeb.net>

_gitroot="git://anongit.freedesktop.org/wayland/libinput"
_gitname="repo-libinput"

pkgname=libinput
pkgver=1.2.2.10.r1946.gbdf4420
pkgrel=1
pkgdesc="Input device management and event handling library"
arch=('i686' 'x86_64')
license=('MIT')
url="http://wayland.freedesktop.org"
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
makedepends=('git' 'expat' 'doxygen')
options=('!libtool')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[libinput_major' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[libinput_minor' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[libinput_micro' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et
