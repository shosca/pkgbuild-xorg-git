# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-vmmouse
_gitname=repo-xf86-input-vmmouse

pkgname=xf86-input-vmmouse
pkgver=13.1.0.2.r132.g8081bdc
pkgrel=161
pkgdesc="X.org VMWare Mouse input driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('systemd')
makedepends=('git' 'xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.1' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.19.99' 'X-ABI-XINPUT_VERSION<24.1')
groups=('xorg-drivers' 'xorg')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr \
    --with-udev-rules-dir=/usr/lib/udev/rules.d
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rfv ${pkgdir}/usr/{lib,share}/hal
}

# vim: ft=sh syn=sh et
