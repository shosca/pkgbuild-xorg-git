# Maintainer: Perry3D <perry3d@gmail.com>

_gitroot="git://anongit.freedesktop.org/mesa/demos"
_gitname="repo-demos"

pkgname=mesa-demos
pkgver=8.3.0.10.r6026.g3e718db
pkgrel=31
pkgdesc="The mesa demos (glxinfo, glxgears, ...), built from the git master branch."
arch=('i686' 'x86_64')
url="http://mesa3d.org/"
license=('LGPL')
depends=('mesa-libgl' 'glew' 'freeglut')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-egl
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et
