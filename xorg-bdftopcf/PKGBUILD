# $Id: PKGBUILD 150979 2012-02-24 12:29:18Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/bdftopcf"
_gitname="repo-bdftopcf"

pkgname=xorg-bdftopcf
pkgver=1.0.5.0.r63.g211c077
pkgrel=0
pkgdesc="Convert X font from Bitmap Distribution Format to Portable Compiled Format"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfont')
makedepends=('xorg-util-macros' 'git')
groups=('xorg' 'xorg-apps')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2-3 | tr - .)" "$(git rev-list HEAD --count)" "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

