# $Id: PKGBUILD 174256 2013-01-03 11:42:05Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdamage"
_gitname="libXdamage"

pkgname=lib32-libxdamage-git
pkgver=r58.41cc41d
pkgrel=81
pkgdesc="X11 damaged region extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes-git' 'libxdamage-git')
makedepends=('pkg-config' 'xorg-util-macros-git' 'git')
provides=('lib32-libxdamage')
conflicts=('lib32-libxdamage')
replaces=('lib32-libxdamage')
options=('!libtool')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

