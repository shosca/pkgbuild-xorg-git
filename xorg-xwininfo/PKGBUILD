# $Id: PKGBUILD 130758 2011-07-07 22:32:22Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xwininfo"
_gitname="repo-xwininfo"

pkgname=xorg-xwininfo
pkgver=1.1.3.5.r107.g0c49f8f
pkgrel=42
pkgdesc="Command-line utility to print information about windows on an X server"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxcb')
makedepends=('xorg-util-macros' 'libx11' 'git')
groups=('xorg-apps' 'xorg')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2-3 | tr - .)" "$(git rev-list HEAD --count)" "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
