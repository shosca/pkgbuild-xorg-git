# $Id: PKGBUILD 130748 2011-07-07 22:27:50Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_realver=1.2.2
_gitroot="git://anongit.freedesktop.org/xorg/app/xprop"
_gitname="xprop"

pkgname=xorg-xprop-git
pkgver=r86.9e0f57f
pkgrel=8
pkgdesc="Property displayer for X"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros-git' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xprop=${_realver}')
conflicts=('xorg-xprop')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

