# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-libinput
_gitname=xf86-input-libinput

pkgname=xf86-input-libinput-git
pkgver=r122.4b2bed6
pkgrel=2
pkgdesc="X.Org libinput driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/xf86-input-libinput/"
license=('custom')
depends=('libinput-git')
makedepends=('xorg-server-devel-git' 'git')
options=('!makeflags')
groups=('xorg-drivers' 'xorg')
provides=("xf86-input-libinput")
conflicts=('xf86-input-libinput')
replaces=('xf86-input-libinput')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  autoreconf -vif
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  install -m755 -d ${pkgdir}/etc/X11/xorg.conf.d
  install -m644 conf/99-libinput.conf ${pkgdir}/etc/X11/xorg.conf.d/
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

