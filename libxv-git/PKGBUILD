# $Id: PKGBUILD 152795 2012-03-09 16:32:25Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXv"
_gitname="libXv"

pkgname=libxv-git
pkgver=r105.cf8cc32
pkgrel=4
pkgdesc="X11 Video extension library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxext-git' 'videoproto-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'git')
provides=('libxv')
conflicts=('libxv')
replaces=('libxv')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-static 
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

