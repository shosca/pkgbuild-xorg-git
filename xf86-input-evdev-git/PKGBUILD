# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-evdev
_gitname=xf86-input-evdev

pkgname=xf86-input-evdev-git
pkgver=r657.034be31
pkgrel=13
pkgdesc="X.Org evdev Input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/xf86-input-evdev/"
license=('custom')
depends=('libevdev-git' 'mtdev')
makedepends=('xorg-server-devel-git' 'git')
options=('!makeflags')
groups=('xorg-drivers' 'xorg')
provides=("xf86-input-evdev")
conflicts=('xf86-input-evdev')
replaces=('xf86-input-evdev')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/xf86-input-evdev/COPYING"
}
# vim: ft=sh syn=sh et

