# Contributor: Thomas Dziedzic <gostrc@@@gmail>
# Contributor: Antti "Tera" Oja <antti.bofh@@@gmail.com>
# Contributor: JiÅ™Ã­ ZÃ¡revÃºcky <zarevucky.jiri@@@gmail.com>

_gitroot='git://git.freedesktop.org/git/xorg/proto/compositeproto'
_gitname='compositeproto'

pkgname=compositeproto-git
pkgver=r52.ef261dc
pkgrel=4
pkgdesc='X11 composite extention protocol specification and header files'
url='http://xorg.freedesktop.org/'
arch=('any')
license=('custom')
depends=('fixesproto-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('compositeproto')
conflicts=('compositeproto')
replaces=('compositeproto')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
