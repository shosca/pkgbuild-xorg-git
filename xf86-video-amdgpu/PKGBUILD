# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-amdgpu'
_gitname="repo-xf86-video-amdgpu"

pkgname=xf86-video-amdgpu
pkgver=1.1.99.22.r186.g7f7f982
pkgrel=2
pkgdesc="X.org amdgpu video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm' 'systemd' 'mesa-dri')
makedepends=('git' 'xorg-server-devel>1.18.98' 'mesa-libgl' 'xf86-input-libinput')
conflicts=('xorg-server<1.18.98' 'X-ABI-VIDEODRV_VERSION<21' 'X-ABI-VIDEODRV_VERSION>=22')
groups=('xorg-drivers' 'xorg')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr \
    --enable-glamor
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
