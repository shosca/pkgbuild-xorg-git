# $Id: PKGBUILD 152771 2012-03-09 16:07:19Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdmcp"
_gitname="libXdmcp"

pkgname=libxdmcp-git
pkgver=r108.9f4cac7
pkgrel=1
pkgdesc="X11 Display Manager Control Protocol library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xproto-git' 'glibc')
makedepends=('xorg-util-macros-git' 'git')
provides=('libxdmcp')
conflicts=('libxdmcp')
replaces=('libxdmcp')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

