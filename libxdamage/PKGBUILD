# $Id: PKGBUILD 174256 2013-01-03 11:42:05Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdamage"
_gitname="repo-libXdamage"

pkgname=libxdamage
pkgver=1.1.4.r58.g41cc41d
pkgrel=90
pkgdesc="X11 damaged region extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'damageproto')
makedepends=('pkg-config' 'xorg-util-macros' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

