# Maintainer: dx <dx@dxzone.com.ar>
# Based on the official cairo package

_gitroot='git://anongit.freedesktop.org/git/cairo'
_gitname='repo-cairo'

pkgname=cairo
pkgver=1.15.2.12.r11314.g747cab7
pkgrel=2
pkgdesc="Cairo vector graphics library (git version)"
arch=('i686' 'x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl' 'lzo'
         'xcb-util' 'glib2' 'libedit')
makedepends=('gtk-doc' 'git' 'mesa-libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind')
options=('!libtool')
provides=('cairo-xcb')
conflicts=('cairo-xcb')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:cairo-version.h | grep 'CAIRO_VERSION_M' \
      | sed -e ':a;N;$!ba;s/\n/./g;s/[^0-9.]*//g;s/\.\.\+//g')" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  #https://code.google.com/p/chromium/issues/detail?id=360604
  export CFLAGS="$CFLAGS -ffat-lto-objects"
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-xcb \
    --enable-tee \
    --enable-gl \
    --enable-gles \
    --enable-egl \
    --disable-static
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et

