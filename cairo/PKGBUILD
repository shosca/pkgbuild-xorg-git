# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>
# Contributor: Serkan Hosca <serkan@hosca.com>

_gitroot='git://anongit.freedesktop.org/git/cairo'
_gitname='repo-cairo'

pkgname=cairo
pkgver=1.15.2.30.r11332.g1272db9
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl' 'lzo')
makedepends=('git' 'mesa-libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind' 'git')
             # for the test suite:
             #'ttf-dejavu' 'gsfonts' 'xorg-server-xvfb' ) # 'libdrm')
#optdepends=('xcb-util: for XCB backend') # really needed?
provides=('cairo-xcb')
replaces=('cairo-xcb')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:cairo-version.h | grep 'CAIRO_VERSION_M' \
      | sed -e ':a;N;$!ba;s/\n/./g;s/[^0-9.]*//g;s/\.\.\+//g')" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--disable-lto \
	--enable-tee \
	--enable-gl \
	--enable-egl \
	--enable-svg \
	--enable-ps \
	--enable-pdf \
	--enable-gobject \
	--enable-gtk-doc
	
	#--disable-xlib-xcb \
	#--enable-test-surfaces \ takes ages
	#--enable-drm # breaks build
	
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et

