# $Id: PKGBUILD 160631 2012-06-02 21:31:52Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_realver=1.0.12
_gitroot="git://anongit.freedesktop.org/xorg/lib/libXaw"
_gitname="libXaw"

pkgname=libxaw-git
pkgver=r182.6234ea0
pkgrel=1
pkgdesc="X11 Athena Widget library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu-git' 'libxpm-git')
makedepends=('xorg-util-macros-git' 'git')
options=('!libtool')
provides=('libxaw=${_realver}')
conflicts=('libxaw')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

