# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

_gitroot="http://llvm.org/git/llvm"
_gitname="repo-llvm"

pkgname=('lib32-llvm' 'lib32-llvm-libs' 'lib32-clang')
pkgver=4.0.0.r139035.ga8c3c60
pkgrel=1
arch=('x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
makedepends=('git' 'cmake' 'lib32-libffi' 'lib32-zlib' 'python2' 'gcc-multilib'
             'lib32-libxml2')
options=('staticlibs')
source=($_gitname::git+$_gitroot
        repo-clang::git+http://llvm.org/git/clang.git
        repo-compiler-rt::git+http://llvm.org/git/compiler-rt.git
)
sha256sums=(
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:CMakeLists.txt | grep 'set(LLVM_VERSION_MAJOR' | sed -e 's/[^0-9]*//g')" \
    "$(git show HEAD:CMakeLists.txt | grep 'set(LLVM_VERSION_MINOR' | sed -e 's/[^0-9]*//g')" \
    "$(git show HEAD:CMakeLists.txt | grep 'set(LLVM_VERSION_PATCH' | sed -e 's/[^0-9]*//g')" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  rm -rf tools/clang
  git clone ${srcdir}/repo-clang tools/clang

  rm -rf projects/compiler-rt
  git clone ${srcdir}/repo-compiler-rt projects/compiler-rt

  mkdir -p build
}

build() {
  cd "$srcdir/$_gitname/build"

  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake \
	-DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVM_LIBDIR_SUFFIX=32 \
    -DCMAKE_C_FLAGS:STRING=-m32 \
    -DCMAKE_CXX_FLAGS:STRING=-m32 \
    -DLLVM_TARGET_ARCH:STRING=i686 \
    -DLLVM_DEFAULT_TARGET_TRIPLE="i686-unknown-linux-gnu" \
    -DLLVM_BUILD_LLVM_DYLIB=ON \
    -DLLVM_DYLIB_EXPORT_ALL=ON \
    -DLLVM_LINK_LLVM_DYLIB=ON \
    -DLLVM_ENABLE_RTTI=ON \
    -DLLVM_ENABLE_FFI=ON \
    -DLLVM_BUILD_DOCS=OFF \
    -DLLVM_ENABLE_SPHINX=OFF \
    -DLLVM_ENABLE_DOXYGEN=OFF \
    -DFFI_INCLUDE_DIR="$(pkg-config --variable=includedir libffi)" \
    -DFFI_LIBRARY_DIR="$(pkg-config --variable=libdir libffi)" \
    -DLLVM_BINUTILS_INCDIR=/usr/include \
    ..

  find .. -type f -exec sed -i 's/\/usr\/lib\/libffi.so/\/usr\/lib32\/libffi.so/g' {} +

  make

  # Disable automatic installation of components that go into subpackages
  sed -i '/\(clang\|lldb\)\/cmake_install.cmake/d' tools/cmake_install.cmake
  sed -i '/extra\/cmake_install.cmake/d' tools/clang/tools/cmake_install.cmake
  sed -i '/compiler-rt\/cmake_install.cmake/d' projects/cmake_install.cmake
}

package_lib32-llvm() {
  pkgdesc="Low Level Virtual Machine (32-bit)"
  depends=('lib32-llvm-libs' 'llvm')
  provides=('lib32-llvm-svn')
  replaces=('lib32-llvm-svn')
  conflicts=('lib32-llvm-svn')

  cd "$srcdir/$_gitname"

  make -C build DESTDIR="$pkgdir" install

  # The runtime library goes into lib32-llvm-libs
  mv "$pkgdir"/usr/lib32/lib{LLVM,LLVM-*,LTO}.so* "$srcdir"
  mv -f "$pkgdir"/usr/lib32/{LLVMgold,BugpointPasses}.so "$srcdir"

  # Fix permissions of static libs
  chmod -x "$pkgdir"/usr/lib32/*.a

  mv "$pkgdir/usr/bin/llvm-config" "$pkgdir/usr/lib32/llvm-config"
  mv "$pkgdir/usr/include/llvm/Config/llvm-config.h" \
    "$pkgdir/usr/lib32/llvm-config-32.h"

  # Get rid of example Hello transformation
  rm "$pkgdir"/usr/lib32/*LLVMHello.*

  rm -rf "$pkgdir"/usr/{bin,include,share/{doc,man,llvm}}

  # Needed for multilib (https://bugs.archlinux.org/task/29951)
  # Header stub is taken from Fedora
  install -d "$pkgdir/usr/include/llvm/Config"
  mv "$pkgdir/usr/lib32/llvm-config-32.h" "$pkgdir/usr/include/llvm/Config/"

  mkdir "$pkgdir"/usr/bin
  mv "$pkgdir/usr/lib32/llvm-config" "$pkgdir/usr/bin/llvm-config32"

  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lib32-llvm-libs() {
  pkgdesc="Low Level Virtual Machine (runtime library)(32-bit) "
  depends=('lib32-libffi' 'lib32-zlib' 'lib32-ncurses' 'lib32-gcc-libs')
  provides=('lib32-llvm-libs-svn')
  replaces=('lib32-llvm-libs-svn')
  conflicts=('lib32-llvm-libs-svn')

  install -d "$pkgdir/usr/lib32"

  cp -P \
    "$srcdir"/lib{LLVM,LLVM-*,LTO}.so* \
    "$srcdir"/{LLVMgold,BugpointPasses}.so \
    "$pkgdir/usr/lib32/"

  # Symlink LLVMgold.so from /usr/lib/bfd-plugins
  # https://bugs.archlinux.org/task/28479
  install -d "$pkgdir/usr/lib32/bfd-plugins"
  ln -s ../LLVMgold.so "$pkgdir/usr/lib32/bfd-plugins/LLVMgold.so"

  install -Dm644 $_gitname/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lib32-clang() {
  pkgdesc="C language family frontend for LLVM"
  url="http://clang.llvm.org/"
  depends=("lib32-llvm=$pkgver-$pkgrel" 'clang' 'gcc-multilib')
  provides=('lib32-clang-svn')
  replaces=('lib32-clang-svn')
  conflicts=('lib32-clang-svn')

  cd "$srcdir/$_gitname"

  make -C build/tools/clang DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,include,share}

  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh et
