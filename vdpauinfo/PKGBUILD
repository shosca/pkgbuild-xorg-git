_gitroot='git://people.freedesktop.org/~aplattner/vdpauinfo'
_gitname='repo-vdpauinfo'

pkgname=vdpauinfo
pkgver=1.0.0.r30.g7b8eee7
pkgrel=42
pkgdesc="CLI utility for VDPAU device querying."
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/~aplattner/vdpauinfo"
license=('custom')
depends=('libvdpau')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

