# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/font/util"
_gitname="repo-util"

pkgname=xorg-font-util
pkgver=1.3.1.0.r77.gcfe0b4f
pkgrel=4
pkgdesc='X.Org font utilities'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
makedepends=('git' 'xorg-util-macros')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

replaces=('font-util')
provides=('font-util')
conflicts=('font-util')
groups=('xorg-fonts' 'xorg')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --with-mapdir=/usr/share/fonts/util \
      --with-fontrootdir=/usr/share/fonts \


  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

