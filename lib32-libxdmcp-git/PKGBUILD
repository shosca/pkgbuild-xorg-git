# $Id: PKGBUILD 152771 2012-03-09 16:07:19Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdmcp"
_gitname="libXdmcp"

pkgname=lib32-libxdmcp-git
pkgver=r111.5021b93
pkgrel=1
pkgdesc="X11 Display Manager Control Protocol library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxdmcp-git' 'xproto-git' 'glibc')
makedepends=('gcc-multilib' 'xorg-util-macros-git' 'git')
provides=('lib32-libxdmcp')
conflicts=('lib32-libxdmcp')
replaces=('lib32-libxdmcp')
options=('!libtool')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

