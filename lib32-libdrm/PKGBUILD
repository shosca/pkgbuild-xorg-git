# Arch libdrm Maintainers:
#Jan de Groot
#Andreas Radke

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='repo-drm'

pkgname=lib32-libdrm
_pkgbasename=${pkgname:6}
pkgver=2.4.75.0.r6017.gd4b83443
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-libpciaccess' $_pkgbasename)
makedepends=(git gcc-multilib xorg-util-macros valgrind-multilib)
url="http://dri.freedesktop.org/"
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac

  ./autogen.sh --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-udev

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ft=sh syn=sh et
