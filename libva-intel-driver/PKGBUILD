# $Id: PKGBUILD 202383 2013-12-21 14:15:26Z bpiotrowski $
# Maintainer : Ionut Biru <ibiru@archlinux.org>

_gitroot="git://anongit.freedesktop.org/vaapi/intel-driver"
_gitname="repo-intel-driver"

pkgname=libva-intel-driver
pkgver=1.6.3.1.r1626.gf6fe842
pkgrel=11
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libva')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_major' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_minor' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_micro' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_pre' | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
