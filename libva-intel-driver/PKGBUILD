# $Id: PKGBUILD 202383 2013-12-21 14:15:26Z bpiotrowski $
# Maintainer : Ionut Biru <ibiru@archlinux.org>

_gitroot="git://anongit.freedesktop.org/vaapi/intel-driver"
_gitname="repo-intel-driver"

pkgname=libva-intel-driver
pkgver=1.6.3.1.r1626.gf6fe842
pkgrel=9
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libva')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | \
            grep "m4_define(\[intel_driver_major_version\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | \
            grep "m4_define(\[intel_driver_minor_version\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | \
            grep "m4_define(\[intel_driver_micro_version\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | \
            grep "m4_define(\[intel_driver_pre_version\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
