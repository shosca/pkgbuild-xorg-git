# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

_gitroot="git://anongit.freedesktop.org/vaapi/intel-driver"
_gitname="repo-intel-driver"

pkgname=libva-intel-driver
pkgver=1.7.3.1.r1748.g1817bee
pkgrel=3
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libva')
makedepends=('git')
replaces=('libva-driver-intel')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_major' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_minor' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_micro' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[intel_driver_pre' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
