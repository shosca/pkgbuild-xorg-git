# Contributor: FoxBuru <FoxBuru at gmail dot com>
# Contributor: Det <nimetonmaili@gmail.com>
# Contributor: SpeedVin <speedvin at archlinux dot us>
# Based on xorg-server-dev

_realver=1.15.99.901
_gitroot="git://anongit.freedesktop.org/xorg/xserver"
_gitname="xserver"

pkgbase=xorg-server-git
pkgname=('xorg-server-git' 'xorg-server-common-git' 'xorg-server-devel-git' 'xorg-server-xdmx-git' 'xorg-server-xephyr-git' 'xorg-server-xnest-git' 'xorg-server-xvfb-git')
pkgver=r13853.c917fa4
pkgrel=2
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('git' 'bigreqsproto-git' 'presentproto-git' 'compositeproto-git'
             'dmxproto-git' 'dri2proto-git' 'dri3proto-git' 'fontsproto-git'
             'glproto-git' 'inputproto-git' 'libdmx-git' 'libgcrypt'
             'libpciaccess-git' 'libx11-git' 'libxaw-git' 'libxfont-git'
             'libxi-git' 'libxkbfile-git' 'libxmu-git' 'libxrender-git'
             'libxres-git' 'libxtst-git' 'libxv-git' 'mesa-libgl-git' 'pixman-git'
             'randrproto-git' 'recordproto-git' 'renderproto-git'
             'resourceproto-git' 'scrnsaverproto-git' 'udev' 'videoproto-git'
             'xcmiscproto-git' 'xextproto-git' 'xf86dgaproto-git' 'libepoxy-git'
             'xf86driproto-git' 'xineramaproto-git' 'xorg-font-util-git'
             'xorg-util-macros-git' 'xorg-xkbcomp-git' 'xtrans-git'
             'xcb-util-git' 'xcb-util-image-git' 'xcb-util-wm-git' 'xcb-util-keysyms-git'
             'libxshmfence-git')
options=('!libtool')
source=($_gitroot
        xvfb-run
        xvfb-run.1
        10-quirks.conf)
sha256sums=('SKIP'
            'ff0156309470fc1d378fd2e104338020a884295e285972cc88e250e031cc35b9'
            '2460adccd3362fefd4cdc5f1c70f332d7b578091fb9167bf88b5f91265bbd776'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  #git merge origin/xwayland -m "xwayland"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
      --prefix=/usr \
      --enable-ipv6 \
      --enable-dmx \
      --enable-dri \
      --enable-dri2 \
      --enable-dri3 \
      --enable-glamor \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-xwayland \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-git() {
  pkgdesc="Xorg server common files - Git ones"
  depends=('xkeyboard-config-git' 'xorg-fonts-misc' 'xorg-setxkbmap-git' 'xorg-xkbcomp-git')
  provides=("xorg-server-common=${_realver}")
  conflicts=('xorg-server-common')

  cd "${srcdir}/${_gitname}"
  make -C xkb DESTDIR="$pkgdir" install-data

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/xorg-server-common/COPYING"
  install -Dm644 man/Xserver.1 "$pkgdir/usr/share/man/man1/Xserver.1"
  install -Dm644 dix/protocol.txt "$pkgdir/usr/lib/xorg/protocol.txt"

  # Don't need this
  rm -f "$pkgdir"/var/lib/xkb/README.compiled
}

package_xorg-server-devel-git() {
  pkgdesc="Development files for the X.Org X server from Git"
  depends=('xproto-git' 'randrproto-git' 'renderproto-git' 
    'xextproto-git' 'inputproto-git' 'kbproto-git'
    'fontsproto-git' 'pixman-git' 'videoproto-git' 
    'xf86driproto-git' 'glproto-git' 'mesa-git'
    'dri2proto-git' 'dri3proto-git' 'xineramaproto-git'
    'libpciaccess-git' 'resourceproto-git' 'scrnsaverproto-git'
    'presentproto-git' 'xorg-util-macros-git')
  provides=("xorg-server-devel=${_realver}")
  conflicts=('xorg-server-devel')

  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/{usr/{bin,share/{man,doc,X11},lib/xorg},var}
  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-devel/"
}

package_xorg-server-xdmx-git() {
  pkgdesc="Distributed Multihead X Server and utilities - Git ones"
  depends=('libdmx-git' 'libgcrypt' 'libxaw-git' 'libxfixes-git' 'libxfont-git'
           'libxi-git' 'libxrender-git' 'pixman-git' 'xorg-server-common-git')
  provides=("xorg-server-xdmx=${_realver}")
  conflicts=('xorg-server-xdmx')

  cd "${srcdir}/${_gitname}/hw/dmx"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xdmx/"
}

package_xorg-server-xephyr-git() {
  pkgdesc="A nested X server that runs as an X application - Git version"
  depends=('libgcrypt' 'libgl' 'libxfont-git' 'libxv-git' 'pixman-git' 'xorg-server-common-git')
  provides=("xorg-server-xephyr=${_realver}")
  conflicts=('xorg-server-xephyr')

  cd "${srcdir}/${_gitname}/hw/kdrive"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xephyr/"
}

package_xorg-server-xnest-git() {
  pkgdesc="A nested X server that runs as an X application - Git version"
  depends=('libgcrypt' 'libxext-git' 'libxfont-git' 'pixman-git' 'xorg-server-common-git')
  provides=("xorg-server-xnest=${_realver}")
  conflicts=('xorg-server-xnest')

  cd "${srcdir}/${_gitname}/hw/xnest"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xnest/"
}

package_xorg-server-xvfb-git() {
  pkgdesc="Virtual framebuffer X server - Git version"
  depends=('libgcrypt' 'libxau-git' 'libxdmcp-git' 'libxfont-git' 'pixman-git' 'xorg-server-common-git')
  provides=("xorg-server-xvfb=${_realver}")
  conflicts=('xorg-server-xvfb')

  cd "${srcdir}/${_gitname}/hw/vfb"
  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/xvfb-run" "$pkgdir/usr/bin/"
  cp "$srcdir/xvfb-run.1" "$pkgdir/usr/share/man/man1/"

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xvfb/"
}

package_xorg-server-git() {
  pkgdesc="Xorg X server - Git version"
  depends=('libdrm-git' 'libgcrypt' 'libpciaccess-git' 'libxshmfence-git'
           'libxau-git' 'libxdmcp-git' 'libepoxy-git' 'libxfont-git'
           'pixman-git' 'udev' 'xf86-input-evdev-git' 'xorg-server-common-git'
           'wayland-git')
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  provides=('x-server' 'xorg-server=${_realver}' 'glamor-egl')
  conflicts=('xorg-server' 'glamor-egl' 'nvidia-utils<=290.10')
  groups=('xorg')

  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/X11"
  mv "$pkgdir/usr/share/X11/xorg.conf.d" "$pkgdir/etc/X11/"
  cp ../10-quirks.conf "$pkgdir/etc/X11/xorg.conf.d/"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir"/usr/lib/xorg/modules/extensions/libglx.{so,xorg}

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server/"

  rm -rf "$pkgdir"/usr/{share/{man/man1/Xserver.1,aclocal,X11},lib/{xorg/protocol.txt,pkgconfig},include}

  # Don't need this
  rm -f "$pkgdir"/var/lib/xkb/README.compiled
}

# vim: ft=sh syn=sh et

