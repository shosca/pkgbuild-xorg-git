# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-mouse

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-mouse
_gitname=xf86-input-mouse

pkgname=xf86-input-mouse-git
pkgver=r242.ccf63a6
pkgrel=3
pkgdesc="X.Org mouse Input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/xf86-input-mouse/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel-git' 'resourceproto-git' 'scrnsaverproto-git' 'git')
groups=('xorg-drivers' 'xorg')
provides=("xf86-input-mouse")
conflicts=('xf86-input-mouse')
replaces=('xf86-input-mouse')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
# vim: ft=sh syn=sh et

