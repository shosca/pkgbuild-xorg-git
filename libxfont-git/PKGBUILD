# $Id: PKGBUILD 151781 2012-03-02 18:55:43Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXfont"
_gitname="libXfont"

pkgname=libxfont-git
pkgver=r288.d338f81
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc-git' 'xproto-git' 'fontsproto-git')
makedepends=('xorg-util-macros-git' 'xtrans-git' 'git')
options=('!libtool')
provides=('libxfont')
conflicts=('libxfont')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

