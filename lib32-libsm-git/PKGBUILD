# $Id: PKGBUILD 151850 2012-03-03 13:43:52Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libSM"
_gitname="libSM"

pkgname=lib32-libsm-git
pkgver=r145.29ea624
pkgrel=4
pkgdesc="X11 Session Management library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libice-git' 'libsm-git' 'lib32-util-linux')
makedepends=('gcc-multilib' 'xorg-util-macros-git' 'xtrans-git' 'git')
options=('!libtool')
provides=('lib32-libsm')
conflicts=('lib32-libsm')
replaces=('lib32-libsm')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

