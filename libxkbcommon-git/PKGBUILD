# Maintainer: Joel Teichroeb <joel@teichroeb.net>

_realver=0.4.0
_gitroot="git://github.com/xkbcommon/libxkbcommon.git"
_gitname="libxkbcommon"

pkgbase=libxkbcommon-git
pkgname=('libxkbcommon-git' 'libxkbcommon-x11-git')
pkgver=r1378.120c5c3
pkgrel=1
pkgdesc="A library to handle keyboard descriptions."
url="http://xkbcommon.org/"
arch=('i686' 'x86_64')
license=('custom')
depends=('xkeyboard-config-git')
makedepends=('git' 'doxygen' 'xorg-util-macros-git' 'libxcb-git')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,lib/pkgconfig}
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package_libxkbcommon-git() {
  provides=('libxkbcommon=${_realver}')
  replaces=('libxkbcommon')
  conflicts=('libxkbcommon')

  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  mv "$pkgdir"/usr/lib/*x11* "$srcdir/x11/usr/lib"
  mv "$pkgdir"/usr/lib/pkgconfig/*x11* "$srcdir/x11/usr/lib/pkgconfig"
  mv "$pkgdir"/usr/include/xkbcommon/*x11* "$srcdir/x11/usr/include/xkbcommon"
}

package_libxkbcommon-x11-git() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients"
  depends=('libxkbcommon-git' 'libxcb-git')

  mv x11/* '$pkgdir'
}
# vim: ft=sh syn=sh et

