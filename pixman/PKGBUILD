# Maintainer: aboe76 AT gmail DOT com>
# contribution: EdwardXXIV

_gitroot="git://anongit.freedesktop.org/git/pixman"
_gitname="repo-pixman"

pkgname=pixman
pkgver=0.35.1.7.r2324.g7c6066b
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('i686' 'x86_64')
url="http://pixman.org/"
license=('GPL')
depends=('glibc')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'pkgconfig')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | grep "m4_define(\[pixman_major\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | grep "m4_define(\[pixman_minor\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | grep "m4_define(\[pixman_micro\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-gtk \
    --disable-static
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}/" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
