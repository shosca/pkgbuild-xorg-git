# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/git/pixman"
_gitname="repo-pixman"

pkgname=pixman
pkgver=0.35.1.23.r2340.g85467ec
pkgrel=3
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org"
license=('custom')
depends=('glibc')
makedepends=('git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep "m4_define(\[pixman_major\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep "m4_define(\[pixman_minor\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git show HEAD:configure.ac | grep "m4_define(\[pixman_micro\]" | tr -d '[]()' | tr -s ' ' | cut -d' ' -f2)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --disable-static
  make
}

check() {
  cd "$srcdir/$_gitname"
  make check
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
