# $Id: PKGBUILD 151848 2012-03-03 13:42:06Z andyrtr $
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libICE"
_gitname="repo-libICE"

pkgname=lib32-libice
pkgver=1.0.9.r145.gb1720ed
pkgrel=9
pkgdesc="X11 Inter-Client Exchange library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('lib32-glibc' 'libice' 'xproto')
makedepends=('gcc-multilib' 'pkgconfig' 'xorg-util-macros' 'xtrans' 'git')
options=('!libtool')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" "$(git describe --long | cut -d "-" -f2 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

