# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

_gitroot='git://github.com/anholt/libepoxy'
_gitname='repo-libepoxy'

pkgname=libepoxy
pkgver=1.3.1.2.r244.g8bbc0d4
pkgrel=178
pkgdesc="Epoxy is a library for handling OpenGL function pointer management for you"
url="https://github.com/anholt/libepoxy"
arch=('i686' 'x86_64')
license=(MIT)
depends=('glibc')
makedepends=('git' 'xorg-util-macros' 'python' 'mesa-libgl')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim: ft=sh syn=sh et
