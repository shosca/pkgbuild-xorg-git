# Maintainer: Riccardo Berto <riccardobrt@gmail.com>
# Previous Contributor: vbmithr
# Contributor: Flamelab <panosfilip at gmail dot com>

_gitroot='git://github.com/anholt/libepoxy'
_gitname='repo-libepoxy'

pkgname=libepoxy
pkgver=1.3.1.2.r244.g8bbc0d4
pkgrel=132
pkgdesc="Epoxy is a library for handling OpenGL function pointer management for you"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'mesa')
makedepends=('git' 'xorg-util-macros' 'python2')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et

