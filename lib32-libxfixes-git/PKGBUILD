# $Id: PKGBUILD 151418 2012-02-26 10:42:43Z pierre $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXfixes"
_gitname="libXfixes"

pkgname=lib32-libxfixes-git
pkgver=r74.1702cdf
pkgrel=81
pkgdesc="X11 miscellaneous 'fixes' extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11-git' 'fixesproto-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('lib32-libxfixes')
conflicts=('lib32-libxfixes')
replaces=('lib32-libxfixes')
options=('!libtool')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

