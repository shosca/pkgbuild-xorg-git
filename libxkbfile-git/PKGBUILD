# $Id: PKGBUILD 152781 2012-03-09 16:19:36Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://anongit.freedesktop.org/xorg/lib/libxkbfile'
_gitname='libxkbfile'

pkgname=libxkbfile-git
pkgver=r82.3b46025
pkgrel=24
pkgdesc="X11 keyboard file manipulation library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('libxkbfile')
conflicts=('libxkbfile')
replaces=('libxkbfile')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static 
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

