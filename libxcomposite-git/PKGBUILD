# $Id: PKGBUILD 174258 2013-01-03 11:50:21Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_realver=0.4.4
_gitroot="git://anongit.freedesktop.org/xorg/lib/libXcomposite"
_gitname="libXcomposite"

pkgname=libxcomposite-git
pkgver=r72.514ae7c
pkgrel=5
pkgdesc="X11 Composite extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes-git')
makedepends=('xorg-util-macros-git' 'compositeproto-git' 'git')
options=('!libtool')
provides=('libxcomposite=${_realver}')
conflicts=('libxcomposite')
replaces=('libxcomposite')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

