# Maintainer : josephgbr <rafael.f.f1@gmail.com>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_realver=0.7
_gitroot="git://people.freedesktop.org/~aplattner/libvdpau"
_gitname="libvdpau"

pkgname=lib32-libvdpau-git
pkgver=r56.186195b
pkgrel=7
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
license=('custom')
depends=('lib32-gcc-libs' "libvdpau-git")
makedepends=('gcc-multilib' 'lib32-libx11-git' 'git')
options=('!libtool')
provides=('lib32-libvdpau=${_realver}')
conflicts=('lib32-libvdpau')
replaces=('lib32-libvdpau')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/include,usr/share}
}
