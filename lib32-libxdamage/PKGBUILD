# $Id: PKGBUILD 174256 2013-01-03 11:42:05Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdamage"
_gitname="repo-libXdamage"

pkgname=lib32-libxdamage
pkgver=1.1.4.4.r58.g41cc41d
pkgrel=89
pkgdesc="X11 damaged region extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes' 'libxdamage')
makedepends=('pkg-config' 'xorg-util-macros' 'git')
options=('!libtool')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

