# $Id: PKGBUILD 174025 2013-01-01 19:16:09Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/libxcb"
_gitname="libxcb"

pkgname=libxcb-git
pkgver=r647.6e0378e
pkgrel=1
pkgdesc="X11 client-side library"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'libxdmcp-git' 'libxau-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'libxslt' 'python2' 'git')
provides=('libxcb')
conflicts=('libxcb')
replaces=('libxcb')
license=('custom')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -e 's/pthread-stubs //g' -i configure.ac

  PYTHON=/usr/bin/python2 
  ./autogen.sh --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  install -m755 -d "${pkgdir}/usr/share/doc/libxcb"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim: ft=sh syn=sh et

