# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_gitroot="git://anongit.freedesktop.org/wayland/wayland"
_gitname="wayland"

pkgname=lib32-wayland-git
pkgver=r1545.e2c0d47
pkgrel=1
pkgdesc='A computer display server protocol (32 bit)'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-libffi' 'wayland-git')
makedepends=('git' 'gcc-multilib' 'lib32-expat')
options=(!libtool)
provides=("lib32-wayland")
conflicts=('lib32-libwayland' 'lib32-wayland')
replaces=('lib32-libwayland' 'lib32-wayland')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --libdir=/usr/lib32 \
    --disable-documentation
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
