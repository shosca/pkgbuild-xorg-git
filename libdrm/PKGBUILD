# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='repo-drm'

pkgname=libdrm
pkgver=2.4.68.7.r5867.g9bdec97
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('git' 'valgrind' 'xorg-util-macros' 'libxslt' 'docbook-xsl')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
url="http://dri.freedesktop.org/"
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  # pthread is useless in Linux
  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install

}

build() {
  cd "${srcdir}/${_gitname}"
  ./configure --prefix=/usr --enable-udev
  make
}

check() {
  cd "$srcdir/$_gitname"
  make -k check
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
