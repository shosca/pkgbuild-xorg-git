# Arch libdrm Maintainers:
#Jan de Groot
#Andreas Radke

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='repo-drm'

pkgname=libdrm
pkgver=2.4.67.11.r5843.gea78c17
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
url="http://dri.freedesktop.org/"
license=('MIT')
depends=('libpciaccess' 'libsystemd')
makedepends=('git' 'xorg-util-macros')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -i "/pthread-stubs/d" configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --enable-udev

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}" install
}
# vim: ft=sh syn=sh et

