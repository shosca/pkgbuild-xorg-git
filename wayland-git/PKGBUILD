# Maintainer: Joel Teichroeb <joel@teichroeb.net>

_gitroot="git://anongit.freedesktop.org/wayland/wayland"
_gitname="wayland"

pkgname=wayland-git
pkgver=r1733.56a5a89
pkgrel=1
pkgdesc="A modern display server."
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org"
license=('MIT')
depends=('libffi')
makedepends=('git' 'expat' 'doxygen')
provides=("wayland")
conflicts=('libwayland' 'wayland')
replaces=('libwayland' 'wayland')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr \
    --disable-static \
    --disable-documentation
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
