# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

_gitroot="git://anongit.freedesktop.org/vaapi/libva"
_gitname="repo-libva"

pkgname=libva
pkgver=1.7.3.r1203.g2ebf897
pkgrel=5
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('git' 'mesa')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
source=("$_gitname::$_gitroot")
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.r%s.%s" \
    "$(($(git show HEAD:configure.ac | grep 'm4_define(\[va_api_major_version\]' | tr -d '[](),' | cut -d' ' -f2)+1))" \
    "$(($(git show HEAD:configure.ac | grep 'm4_define(\[va_api_minor_version\]' | tr -d '[](),' | cut -d' ' -f2)-32))" \
    "3" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
