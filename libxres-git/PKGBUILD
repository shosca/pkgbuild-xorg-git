# $Id: PKGBUILD 152788 2012-03-09 16:26:13Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_realver=1.1.99.0
_gitroot="git://anongit.freedesktop.org/xorg/lib/libXRes"
_gitname="libXRes"

pkgname=libxres-git
pkgver=r83.ae0ddaa
pkgrel=1
pkgdesc="X11 Resource extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('libxext-git')
makedepends=('resourceproto-git' 'damageproto-git' 'compositeproto-git' 'scrnsaverproto' 'xorg-util-macros-git' 'git')
options=('!libtool')
provides=('libxres=${_realver}')
conflicts=('libxres')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

