# Maintainer: Riccardo Berto <riccardobrt@gmail.com>
# Previous Contributor: vbmithr
# Contributor: Flamelab <panosfilip at gmail dot com>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-ati'
_gitname="repo-xf86-video-ati"

pkgname=xf86-video-ati
pkgver=7.6.99.r3285.g7bc089e
pkgrel=1
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('libpciaccess' 'libdrm' 'udev' 'pixman' 'mesa')
makedepends=('xorg-server-devel' 'xf86driproto' 'git' 'glproto')
conflicts=('xorg-server<1.15.99')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.g%s" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | \
        sed -e '/AC_INIT/,/xf86-video-ati/!d' | tr '[]{}(),' ' ' | tr '\n' ' ' | tr -s ' ' | cut -d' ' -f3)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --enable-glamor
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install 
}

# vim: ft=sh syn=sh et

