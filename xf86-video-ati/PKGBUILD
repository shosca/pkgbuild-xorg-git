# Maintainer: Riccardo Berto <riccardobrt@gmail.com>
# Previous Contributor: vbmithr
# Contributor: Flamelab <panosfilip at gmail dot com>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-ati'
_gitname="repo-xf86-video-ati"

pkgname=xf86-video-ati
pkgver=7.6.99.37.r3306.g8a6cd4b
pkgrel=23
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('libpciaccess' 'libdrm' 'udev' 'pixman' 'mesa')
makedepends=('xorg-server-devel' 'xf86driproto' 'git' 'glproto')
conflicts=('xorg-server<1.15.99')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --enable-glamor
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install 
}

# vim: ft=sh syn=sh et

