# Maintainer: kfgz <kfgz at interia pl>
# Contributor: Cilyan Olowen <gaknar at gmail dot com>

_gitroot="git://git.freedesktop.org/git/xorg/proto/glproto"
_gitname="glproto"

pkgname=glproto-git
pkgver=r84.bd3d751
pkgrel=2
pkgdesc="X11 OpenGL extension wire protocol"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-util-macros-git' 'git')
provides=('glproto')
conflicts=('glproto')
replaces=('glproto')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/${_gitname-build}/COPYING \
                "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ft=sh syn=sh et

