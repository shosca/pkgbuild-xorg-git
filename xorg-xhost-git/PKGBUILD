# $Id: PKGBUILD 154150 2012-03-23 19:14:18Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xhost"
_gitname="xhost"

pkgname=xorg-xhost-git
pkgver=r87.06d7137
pkgrel=1
pkgdesc="Server access control program for X"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11-git' 'xtrans-git' 'libxmu-git')
makedepends=('xorg-util-macros-git' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xhost')
conflicts=('xorg-xhost')
replaces=('xorg-xhost')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

