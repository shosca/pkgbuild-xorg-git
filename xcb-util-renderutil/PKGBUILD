# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/util-renderutil"
_gitname="repo-util-renderutil"

pkgname=xcb-util-renderutil
pkgver=0.3.9.0.r70.g0317caf
pkgrel=37
pkgdesc="Utility libraries for XC Binding - Convenience functions for the Render extension"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7')
makedepends=('git' 'xorg-util-macros')
source=($_gitname::$_gitroot 'LICENSE')
sha256sums=('SKIP'
            'ded299aa179dcf0d885bf89274a4db77a530e03f9f5e7cf1c3c4ef1d60e914b9')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  git submodule update --init --recursive

  ./autogen.sh --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${_gitname}"
  make check
}

package() { 
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
