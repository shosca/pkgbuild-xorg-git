# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Serkan Hosca <serkan@hosca.com>

_gitroot='git://git.freedesktop.org/git/xorg/proto/dri3proto'
_gitname='repo-dri3proto'

pkgname=dri3proto
pkgver=1.0.7.r23.g91df0f8
pkgrel=1
pkgdesc="X11 DRI3 protocol"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-util-macros' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et
