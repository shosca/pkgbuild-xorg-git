# $Id: PKGBUILD 152786 2012-03-09 16:24:03Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXrender"
_gitname="libXrender"

pkgname=libxrender-git
pkgver=r74.7887382
pkgrel=39
pkgdesc="X Rendering Extension client library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11-git' 'renderproto-git')
provides=('libxrender')
conflicts=('libxrender')
makedepends=('git' 'xorg-util-macros-git' 'pkgconfig')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

