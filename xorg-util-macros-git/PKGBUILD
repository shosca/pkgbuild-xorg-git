# $Id: PKGBUILD 153461 2012-03-14 17:38:45Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/util/macros"
_gitname="macros"

pkgname=xorg-util-macros-git
pkgver=r190.9a54b85
pkgrel=1
pkgdesc="X.Org Autotools macros"
arch=('any')
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('git')
provides=('xorg-util-macros')
conflicts=('xorg-util-macros')
replaces=('xorg-util-macros')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

