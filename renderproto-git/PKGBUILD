# Contributor: Thomas Dziedzic <gostrc@@@gmail>
# Contributor: Antti "Tera" Oja <antti.bofh@@@gmail.com>
# Contributor: JiÅ™Ã­ ZÃ¡revÃºcky <zarevucky.jiri@@@gmail.com>

_gitroot='git://git.freedesktop.org/git/xorg/proto/renderproto'
_gitname='renderproto'

pkgname=renderproto-git
pkgver=r68.9c2a896
pkgrel=2
pkgdesc='X11 composite extention protocol specification and header files'
url='http://xorg.freedesktop.org/'
arch=('any')
license=('custom')
makedepends=('xorg-util-macros-git' 'git')
provides=('renderproto')
conflicts=('renderproto')
replaces=('renderproto')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ft=sh syn=sh et
