# $Id: PKGBUILD 151848 2012-03-03 13:42:06Z andyrtr $
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libICE"
_gitname="libICE"

pkgname=lib32-libice-git
pkgver=r142.0dfab42
pkgrel=3
pkgdesc="X11 Inter-Client Exchange library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('lib32-glibc' 'libice-git' 'xproto-git')
makedepends=('gcc-multilib' 'pkgconfig' 'xorg-util-macros-git' 'xtrans-git' 'git')
options=('!libtool')
license=('custom')
provides=('lib32-libice')
conflicts=('lib32-libice')
replaces=('lib32-libice')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

