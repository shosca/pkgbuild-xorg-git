# $Id: $
# Maintainer: Jan de Groot

_realver=0.6
_gitroot="git://anongit.freedesktop.org/libevdev"
_gitname="libevdev"


pkgname=libevdev-git
pkgver=r426.1c425e9
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/libevdev/"
license=(custom:X11)
depends=('glibc')
makedepends=('git' 'python')
provides=('libevdev=${_realver}')
conflicts=('libevdev')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-static --disable-gcov
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
