# Maintainer: Armin K. <krejzi at email dot com>

_gitroot='git://github.com/llvm-mirror/libclc.git'
_gitname=libclc

pkgname=libclc-git
pkgver=r233.0a2d161
pkgrel=3
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('i686' 'x86_64')
url="http://libclc.llvm.org/"
license=('MIT')
makedepends=('clang-git' 'git' 'python2')
provides=('libclc')
replaces=('libclc')
conflicts=('libclc')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  /usr/bin/python2 ./configure.py \
    --prefix=/usr \
    --with-llvm-config=/usr/bin/llvm-config
  sed -i 's/ python / python2 /' Makefile
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

