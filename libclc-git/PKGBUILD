# Maintainer: Armin K. <krejzi at email dot com>

_gitroot='git://github.com/llvm-mirror/libclc.git'
_gitname=libclc

pkgname=libclc-git
pkgver=r173.78264ec
pkgrel=1
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('i686' 'x86_64')
url="http://libclc.llvm.org/"
license=('MIT')
depends=(llvm-git llvm-libs-git llvm-ocaml-git clang-git clang-analyzer-git)
makedepends=('git' 'python2')
provides=('libclc')
replaces=('libclc')
conflicts=('libclc')
source=($_gitroot llvm_buildfix.patch)
sha256sums=('SKIP'
            'cd03f864291261ae83ca1a8160361105f1f04b1b088561f9da5ed60a5a02767a')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  patch -Np1 -i ../llvm_buildfix.patch
  /usr/bin/python2 ./configure.py \
    --prefix=/usr \
    --with-llvm-config=/usr/bin/llvm-config
  sed -i 's/ python / python2 /' Makefile
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

