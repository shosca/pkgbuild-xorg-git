# Maintainer: Armin K. <krejzi at email dot com>

_gitroot='git://github.com/llvm-mirror/libclc.git'
_gitname=libclc

pkgname=libclc-git
pkgver=r141.22a202d
pkgrel=2
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('i686' 'x86_64')
url="http://libclc.llvm.org/"
license=('MIT')
depends=(llvm-git llvm-libs-git llvm-ocaml-git clang-git clang-analyzer-git)
makedepends=('git' 'python2')
provides=('libclc')
replaces=('libclc')
conflicts=('libclc')
source=($_gitroot fix-llvm-remove-owningptr.patch)
sha256sums=('SKIP'
            '1eb75be56afeb0e08bddc904568367b7db86449c6fade60e2a44763f6e477f5b')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  patch -Np1 -i ../fix-llvm-remove-owningptr.patch
  /usr/bin/python2 ./configure.py \
    --prefix=/usr \
    --with-llvm-config=/usr/bin/llvm-config
  sed -i 's/ python / python2 /' Makefile
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

