# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-libinput
_gitname=repo-xf86-input-libinput

pkgname=xf86-input-libinput
pkgver=0.17.0.4.r162.g5e7ee73
pkgrel=10
pkgdesc="X.Org libinput driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/xf86-input-libinput/"
license=('custom')
depends=('libinput')
makedepends=('xorg-server-devel' 'git')
options=('!makeflags')
groups=('xorg-drivers' 'xorg')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  autoreconf -vif
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d ${pkgdir}/etc/X11/xorg.conf.d
  install -m644 conf/90-libinput.conf ${pkgdir}/etc/X11/xorg.conf.d/
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

