# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xev"
_gitname="repo-xev"

pkgname=xorg-xev
pkgver=1.2.2.0.r72.ga92082c
pkgrel=55
pkgdesc='Print contents of X events'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
depends=('libx11' 'libxrandr')
makedepends=('git' 'xorg-util-macros' 'xproto')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')
groups=('xorg-apps' 'xorg')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | sed -e ':a;N;$!ba;s/\n//g;s/[^0-9.]*//g;s/\.\.\+//g;s/\s+//g')" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
