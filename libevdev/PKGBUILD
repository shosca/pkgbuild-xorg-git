# $Id: $
# Maintainer: Jan de Groot

_gitroot="git://anongit.freedesktop.org/libevdev"
_gitname="repo-libevdev"


pkgname=libevdev
pkgver=1.4.6.2.r528.g46b8381
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/libevdev/"
license=(custom:X11)
depends=('glibc')
makedepends=('git' 'python')
source=($_gitname::$_gitroot fix-ui-get-sysname.patch)
sha256sums=('SKIP'
            'fff1d056fdb9e378c33e9ca08a49e14cc67ce9f97ea80f0b809c058441411129')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  patch -Np1 -i ../fix-ui-get-sysname.patch

  ./autogen.sh --prefix=/usr --disable-static --disable-gcov
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
