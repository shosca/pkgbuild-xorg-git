# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_gitroot="git://anongit.freedesktop.org/libevdev"
_gitname="repo-libevdev"

pkgname=libevdev
pkgver=1.5.5.0.r556.g452847a
pkgrel=1
pkgdesc="Wrapper library for evdev devices"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/libevdev/"
license=(custom:X11)
depends=('glibc')
makedepends=('git' 'python2' 'check' 'valgrind' 'doxygen')
source=("$_gitname::$_gitroot")
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --disable-static

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
