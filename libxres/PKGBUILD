# $Id: PKGBUILD 152788 2012-03-09 16:26:13Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXRes"
_gitname="repo-libXRes"

pkgname=libxres
pkgver=1.1.99.0.r83.gae0ddaa
pkgrel=89
pkgdesc="X11 Resource extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
license=('custom')
depends=('libxext')
makedepends=('resourceproto' 'damageproto' 'compositeproto' 'scrnsaverproto' 'xorg-util-macros' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.%s" \
    "$(git archive --remote=$srcdir/$_gitname HEAD configure.ac | tar -xO | \
                           grep AC_INIT | tr -d '[](),' | cut -d' ' -f2)" \
    "$(git rev-list HEAD --count)" \
    "$(git describe --long | cut -d "-" -f4)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

