# $Id: PKGBUILD 152267 2012-03-06 08:39:49Z allan $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://git.freedesktop.org/git/xorg/font/encodings'
_gitname='encodings'

pkgname=xorg-fonts-encodings-git
pkgver=r60.11ca3cf
pkgrel=12
pkgdesc="X.org font encoding files"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-mkfontscale-git' 'xorg-util-macros-git' 'xorg-font-util-git' 'git')
groups=('xorg-fonts' 'xorg')
options=(!makeflags)
provides=('xorg-fonts-encodings')
conflicts=('xorg-fonts-encodings')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # regenerate encodngs file not to include non-compressed versions
  cd $pkgdir/usr/share/fonts/encodings/large
  mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings/large -e . .
  cd $pkgdir/usr/share/fonts/encodings/
  mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings -e . -e large .
}

# vim: ft=sh syn=sh et
