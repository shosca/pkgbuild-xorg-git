# Maintainer: dx <dx@dxzone.com.ar>
# Based on the official cairo package

_gitroot='git://anongit.freedesktop.org/git/cairo'
_gitname='cairo'

pkgname=cairo-git
pkgver=r11175.91f128b
pkgrel=3
pkgdesc="Cairo vector graphics library (git version)"
arch=('i686' 'x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender-git' 'fontconfig' 'pixman-git' 'glib2' 'sh' 'lzo2'
         'xcb-util-git' 'glib2>=2.24.0')
makedepends=('gtk-doc' 'git' 'mesa-libgl-git')
options=('!libtool')
provides=('cairo' 'cairo-xcb')
replaces=('cairo')
conflicts=('cairo' 'cairo-xcb')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  #https://code.google.com/p/chromium/issues/detail?id=360604
  export CFLAGS="$CFLAGS -ffat-lto-objects"
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-xcb \
    --enable-tee \
    --enable-gl \
    --enable-gles \
    --enable-egl \
    --disable-static
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh syn=sh et

