# Maintainer: aboe76 AT gmail DOT com>
# contribution: EdwardXXIV

_gitroot="git://anongit.freedesktop.org/git/pixman"
_gitname="pixman"

pkgname=pixman-git
pkgver=r2249.58e21d3
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=('i686' 'x86_64')
url="http://pixman.org/"
license=('GPL')
depends=('glibc')
makedepends=('git' 'autoconf' 'automake' 'libtool' 'pkgconfig')
provides=('pixman')
conflicts=('pixman')
replaces=('pixman')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-gtk \
    --disable-static
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
