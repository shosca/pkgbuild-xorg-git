# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
_gitroot="git://anongit.freedesktop.org/vaapi/vdpau-driver"
_gitname="repo-vdpau-driver"

pkgname=libva-vdpau-driver
pkgver=0.7.5.1.r388.g3dda0b4
pkgrel=535
pkgdesc="VDPAU backend for VA API"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('GPL')
depends=('libva' 'libvdpau' 'libgl')
makedepends=('git' 'mesa')
replaces=('vdpau-video')
source=($_gitname::$_gitroot
        libva-vdpau-driver-0.7.4-glext-missing-definition.patch
        libva-vdpau-driver-0.7.4-libvdpau-0.8.patch
        libva-vdpau-driver-0.7.4-VAEncH264VUIBufferType.patch)
sha256sums=('SKIP'
            '776bfe4c101cdde396d8783029b288c6cd825d0cdbc782ca3d94a5f9ffb4558c'
            '5e567b026b97dc0e207b6c05410cc1b7b77a58ceb5046801d0ea1a321cba3b9d'
            '1ae32b8e5cca1717be4a63f09e8c6bd84a3e9b712b933816cdb32bb315dbda98')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[vdpau_video_major' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[vdpau_video_minor' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[vdpau_video_micro' | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git show HEAD:configure.ac | grep 'm4_define(\[vdpau_video_pre'   | sed -e 's/m4_//g;s/\s+//g;s/[^0-9]*//g')" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"

  patch -p1 -i ../libva-vdpau-driver-0.7.4-glext-missing-definition.patch
  patch -p1 -i ../libva-vdpau-driver-0.7.4-libvdpau-0.8.patch
  patch -p1 -i ../libva-vdpau-driver-0.7.4-VAEncH264VUIBufferType.patch
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m644 -D COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
# vim:set ts=2 sw=2 et:
