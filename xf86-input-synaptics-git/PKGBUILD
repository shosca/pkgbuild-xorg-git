# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-synaptics

_gitroot=git://anongit.freedesktop.org/xorg/driver/xf86-input-synaptics
_gitname=xf86-input-synaptics

pkgname=xf86-input-synaptics-git
pkgver=r1448.ddd8844
pkgrel=109
pkgdesc="X.Org synaptics Input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/xf86-input-synaptics/"
license=('custom')
depends=('mtdev' 'libevdev-git' 'libxi-git' 'libxtst-git')
makedepends=('xorg-server-devel-git' 'resourceproto-git' 'scrnsaverproto-git' 'git')
groups=('xorg-drivers' 'xorg')
provides=('xf86-input-synaptics')
conflicts=('xf86-input-synaptics' 'xorg-server<1.15.99')
replaces=('xf86-input-synaptics')
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=($_gitroot 10-synaptics.conf)
sha256sums=('SKIP'
            'bd506a0545c335faa6da7ec42b3f60f2c09b5ed9e26f03003d6f59be88107ee2')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-unit-tests
  make
}

package(){
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}

# vim: ft=sh syn=sh et
