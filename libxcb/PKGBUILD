# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/libxcb"
_gitname="repo-libxcb"

pkgname=libxcb
pkgver=1.11.90.65.r652.g095353f
pkgrel=2
pkgdesc="X11 client-side library"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.11' 'libxdmcp' 'libxau')
makedepends=('git' 'libxslt' 'python2' 'xorg-util-macros')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  sed -e 's/pthread-stubs //g' -i configure.ac
  autoreconf -vfi
}

build() {
  cd "${srcdir}/${_gitname}"
  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

check() {
  cd "${srcdir}/${_gitname}"
  make -k check
}

package() {
  cd "${srcdir}/${_gitname}"
  install -m755 -d "${pkgdir}/usr/share/doc/libxcb"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ft=sh syn=sh et
