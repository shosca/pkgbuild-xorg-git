# $Id: PKGBUILD 174256 2013-01-03 11:42:05Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdamage"
_gitname="libXdamage"

pkgname=libxdamage-git
pkgver=r58.41cc41d
pkgrel=26
pkgdesc="X11 damaged region extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes-git' 'damageproto-git')
makedepends=('pkg-config' 'xorg-util-macros-git' 'git')
provides=('libxdamage')
conflicts=('libxdamage')
replaces=('libxdamage')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
# vim: ft=sh syn=sh et

