# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_gitroot="git://anongit.freedesktop.org/wayland/weston"
_gitname="weston"

pkgname=weston-git
pkgver=r4282.41bdc27
pkgrel=1
pkgdesc="Reference implementation of a Wayland compositor"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org"
license=('MIT')
depends=('libxkbcommon-git' 'wayland-git' 'mesa-git' 'cairo-git' 'libunwind'
         'poppler-glib' 'mtdev' 'libinput-git' 'libxcursor-git' 'pixman-git'
         'glu-git' 'freerdp-git' 'libice-git')
makedepends=('git')
provides=('weston')
conflicts=('weston')
replaces=('weston')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/weston \
    --enable-libinput-backend \
    --enable-xwayland \
    --enable-rdp-compositor \
    --disable-static \
    --disable-setuid-install \
    --disable-android-compositor

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
