# $Id: PKGBUILD 152795 2012-03-09 16:32:25Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXv"
_gitname="libXv"

pkgname=lib32-libxv-git
pkgver=r104.736d7ac
pkgrel=22
pkgdesc="X11 Video extension library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext-git' 'libxv-git')
makedepends=('pkgconfig' 'xorg-util-macros-git' 'git')
options=('!libtool')
provides=('lib32-libxv')
conflicts=('lib32-libxv')
replaces=('lib32-libxv')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-static 
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ft=sh syn=sh et

