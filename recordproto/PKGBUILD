# Contributor: Thomas Dziedzic <gostrc@@@gmail>
# Contributor: Antti "Tera" Oja <antti.bofh@@@gmail.com>
# Contributor: JiÅ™Ã­ ZÃ¡revÃºcky <zarevucky.jiri@@@gmail.com>

_gitroot='git://git.freedesktop.org/git/xorg/proto/recordproto'
_gitname='repo-recordproto'

pkgname=recordproto
pkgver=1.14.2.3.r56.gc414387
pkgrel=8
pkgdesc='X11 composite extention protocol specification and header files'
url='http://xorg.freedesktop.org/'
arch=('any')
license=('custom')
makedepends=('xorg-util-macros' 'git')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
