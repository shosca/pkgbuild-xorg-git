# $Id: PKGBUILD 174025 2013-01-01 19:16:09Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/libxcb"
_gitname="libxcb"

pkgname=lib32-libxcb-git
pkgver=r576.1251354
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'libxcb-git' 'lib32-libxdmcp-git' 'lib32-libxau-git')
makedepends=('gcc-multilib' 'pkgconfig' 'libxslt' 'python2' 'xorg-util-macros-git' 'git')
provides=('lib32-libxcb')
conflicts=('lib32-libxcb')
options=('!libtool')
license=('custom')
source=($_gitroot libxcb-1.1-no-pthread-stubs.patch)
sha256sums=('SKIP'
            'ae17e24d91c0f0e5ee86e96d9e9d4332b9b64e21523e8f6f3b6ed2de41df137d')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  patch -Np1 -i ../libxcb-1.1-no-pthread-stubs.patch

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export PYTHON=/usr/bin/python2 

  ./autogen.sh \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
# vim: ft=sh syn=sh et

