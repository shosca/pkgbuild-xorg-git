# $Id: PKGBUILD 168377 2012-10-09 16:18:36Z andyrtr $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_realver=1.10
_gitroot="git://anongit.freedesktop.org/xcb/proto"
_gitname="proto"

pkgname=xcb-proto-git
pkgver=r343.389889e
pkgrel=1
pkgdesc="XML-XCB protocol descriptions"
arch=('any')
url="http://xcb.freedesktop.org/"
license=('custom')
makedepends=('python2' 'libxml2' 'git')
provides=('xcb-proto=${_realver}')
conflicts=('xcb-proto')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim: ft=sh syn=sh et
