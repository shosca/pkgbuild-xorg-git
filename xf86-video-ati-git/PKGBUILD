# Maintainer: Riccardo Berto <riccardobrt@gmail.com>
# Previous Contributor: vbmithr
# Contributor: Flamelab <panosfilip at gmail dot com>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-ati'
_gitname="xf86-video-ati"

pkgname=xf86-video-ati-git
pkgver=r3144.fbf575c
pkgrel=4
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('libpciaccess-git' 'libdrm-git' 'udev' 'pixman-git' 'ati-dri-git')
makedepends=('xorg-server-git' 'xorg-server-devel-git' 'xf86driproto-git' 'git' 'glproto-git')
provides=('xf86-video-ati')
conflicts=('xf86-video-ati' 'xorg-server<1.15.99')
replaces=('xf86-video-ati')
license=('custom')
source=($_gitroot
        http://people.freedesktop.org/~agd5f/0001-radeon-enable-hawaii-accel-conditionally.patch)
sha256sums=('SKIP'
            '98fc10035e047ed3e9b672ba86364336b8988787caf0cb421e267e1dc81c01ef')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  patch -Np1 -i ../0001-radeon-enable-hawaii-accel-conditionally.patch

  ./autogen.sh --prefix=/usr --enable-glamor
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install 
}

# vim: ft=sh syn=sh et

