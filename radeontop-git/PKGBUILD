# Maintainer: Antoine Lubineau <antoine@lubignon.info>

_gitroot='git://github.com/clbr/radeontop.git'
_gitname='radeontop'

pkgname=radeontop-git
pkgver=r154.575a416
pkgrel=1
pkgdesc='Radeon cards monitoring utility'
arch=('i686' 'x86_64')
url='https://github.com/clbr/radeontop'
license=('GPL3')
depends=('ncurses' 'libpciaccess-git' 'libdrm-git')
makedepends=('git')
provides=('radeontop')
conflicts=('radeontop')
replaces=('radeontop')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"
  sed -i "s/sbin/bin/" Makefile
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir/" install
}

# vim: ft=sh syn=sh et
