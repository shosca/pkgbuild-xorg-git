# $Id: PKGBUILD 151418 2012-02-26 10:42:43Z pierre $
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXfixes"
_gitname="repo-libXfixes"

pkgname=libxfixes
pkgver=5.0.1.r74.g1702cdf
pkgrel=90
pkgdesc="X11 miscellaneous 'fixes' extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'fixesproto')
makedepends=('xorg-util-macros' 'git')
license=('custom')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.g%s" \
    "$(git describe --long | cut -d "-" -f2 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

