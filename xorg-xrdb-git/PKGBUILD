# $Id: PKGBUILD 154150 2012-03-23 19:14:18Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/app/xrdb"
_gitname="xrdb"

pkgname=xorg-xrdb-git
pkgver=r100.3ae3e2f
pkgrel=2
pkgdesc="X.Org authorization settings program"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu-git' 'mcpp')
makedepends=('xorg-util-macros-git' 'git')
groups=('xorg-apps' 'xorg')
provides=('xorg-xrdb')
conflicts=('xorg-xrdb')
replaces=('xorg-xrdb')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et

