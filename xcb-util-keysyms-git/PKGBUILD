# $Id: PKGBUILD 160619 2012-06-02 21:15:01Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xcb/util-keysyms"
_gitname="util-keysyms"

pkgname=xcb-util-keysyms-git
pkgver=r64.ae88512
pkgrel=16
pkgdesc="Utility libraries for XC Binding - Standard X key constants and conversion to/from keycodes"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb-git')
makedepends=('xorg-util-macros-git' 'git')
provides=('xcb-util-keysyms')
conflicts=('xcb-util-keysyms')
replaces=('xcb-util-keysyms')
source=($_gitroot 'LICENSE')
sha256sums=('SKIP'
            'ded299aa179dcf0d885bf89274a4db77a530e03f9f5e7cf1c3c4ef1d60e914b9')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  git submodule init && git submodule update

  ./autogen.sh --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${_gitname}"
  make check
}

package() { 
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ft=sh syn=sh et
