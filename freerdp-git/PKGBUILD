# $Id: PKGBUILD 109311 2014-04-13 09:23:06Z fyan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

_gitroot="git://github.com/freerdp/freerdp"
_gitname="freerdp"

pkgname=freerdp-git
pkgver=r8435.423b151
pkgrel=1
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor-git' 'libcups' 'alsa-lib' 'libxext-git'
      'libxdamage-git' 'ffmpeg' 'libxkbfile-git' 'libxinerama-git'
      'libxv-git' 'wayland-git' 'libpulse')
makedepends=('krb5' 'cmake' 'damageproto-git' 'xmlto' 'docbook-xsl' 'git')
conflicts=('freerdp')
replaces=('freerdp')
provides=('freerdp')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_PULSEAUDIO=ON \
    -DWITH_WAYLAND=OFF \
    .

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="${pkgdir}" install
}
