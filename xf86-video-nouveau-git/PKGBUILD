# $Id: PKGBUILD 174803 2013-01-07 18:10:24Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

_gitroot="git://anongit.freedesktop.org/nouveau/xf86-video-nouveau"
_gitname="xf86-video-nouveau"

pkgname=xf86-video-nouveau-git
pkgver=r2435.5212dbd
pkgrel=2
pkgdesc="Open Source 2D acceleration driver for nVidia cards"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/"
license=('GPL')
depends=('libpciaccess-git' 'libdrm-git' 'udev' 'pixman-git' 'mesa-git')
makedepends=('xorg-server-devel-git' 'xf86driproto-git' 'git' 'glproto-git')
provides=('xf86-video-nouveau')
conflicts=('xf86-video-nouveau' 'xorg-server<1.15.99')
replaces=('xf86-video-nouveau')
groups=('xorg-drivers' 'xorg')
install=$pkgname.install
source=($_gitroot
        glamor-build-fix.patch)
sha256sums=('SKIP'
            '441300d666484703eb722e93a2fdfc5ed04ea201d62b4a290971fe70454d9c29')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  patch -Np1 -i ../glamor-build-fix.patch

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et

