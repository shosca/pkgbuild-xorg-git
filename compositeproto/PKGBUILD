# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Serkan Hosca <serkan@hosca.com>

_gitroot='git://git.freedesktop.org/git/xorg/proto/compositeproto'
_gitname='repo-compositeproto'

pkgname=compositeproto
pkgver=0.4.2.4.r52.gef261dc
pkgrel=1
pkgdesc="X11 Composite extension wire protocol"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('fixesproto')
makedepends=('git' 'xorg-util-macros')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.r%s.g%s" \
    "$(git show HEAD:configure.ac | grep -Pzo 'AC_INIT\((?s)(.*?)\)' \
      | tr '[](),\n' ' ' | tr -s ' ' | cut -d' ' -f 3)" \
    "$(git rev-list $(git rev-list --tags --no-walk --max-count=1)..HEAD --count)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et
