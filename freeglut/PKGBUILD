# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Serkan Hosca <serkan@hosca.com>

_gitroot="git://github.com/dcnieho/FreeGLUT"
_gitname="repo-freeglut"

pkgname=freeglut
pkgver=3.0.0.r1861.gc59188a
pkgrel=71
pkgdesc="Provides functionality for small OpenGL programs"
arch=('i686' 'x86_64')
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=('libxi' 'libxrandr' 'libgl')
makedepends=('git' 'mesa' 'glu' 'libxxf86vm' 'cmake')
replaces=('glut')
provides=('glut')
conflicts=('glut')
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  printf "%s.%s.%s.r%s.g%s" \
    "$(git show HEAD:freeglut/freeglut/CMakeLists.txt | grep 'set(VERSION_MAJOR' | tr -s ' ' | tr -d '[]()' | cut -d' ' -f2)" \
    "$(git show HEAD:freeglut/freeglut/CMakeLists.txt | grep 'set(VERSION_MINOR' | tr -s ' ' | tr -d '[]()' | cut -d' ' -f2)" \
    "$(git show HEAD:freeglut/freeglut/CMakeLists.txt | grep 'set(VERSION_PATCH' | tr -s ' ' | tr -d '[]()' | cut -d' ' -f2)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname/$pkgname/$pkgname"

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_INSTALL_LIBDIR:PATH=/usr/lib .
  make
}

package() {
  cd "$srcdir/$_gitname/$pkgname/$pkgname"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
