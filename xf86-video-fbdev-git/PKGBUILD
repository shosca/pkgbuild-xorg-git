# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-fbdev'
_gitname='xf86-video-fbdev'

pkgname=xf86-video-fbdev-git
pkgver=r101.ae0aeff
pkgrel=202
pkgdesc='X.org framebuffer video driver'
arch=('i686' 'x86_64')
url='http://xorg.freedesktop.org/'
license=('custom')
depends=('glibc')
makedepends=('git' 'xorg-server-devel-git')
provides=('xf86-video-fbdev')
conflicts=('xf86-video-fbdev')
replaces=('xf86-video-fbdev')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install 

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim: ft=sh syn=sh et

