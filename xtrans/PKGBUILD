# $Id: PKGBUILD 154130 2012-03-23 17:11:54Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_gitroot="git://anongit.freedesktop.org/xorg/lib/libxtrans"
_gitname="repo-libxtrans"

pkgname=xtrans
pkgver=1.3.5.3.r243.gcc29fbc
pkgrel=3
pkgdesc="X transport library"
arch=('any')
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('xorg-util-macros' 'git')
options=(!emptydirs)
source=($_gitname::$_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s.g%s" \
    "$(git describe --long | cut -d "-" -f2-3 | tr - .)" \
    "$(git rev-list HEAD --count)" \
    "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -m755 -d "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib/pkgconfig"
}

# vim: ft=sh syn=sh et
