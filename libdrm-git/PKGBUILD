# Arch libdrm Maintainers:
#Jan de Groot
#Andreas Radke

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

pkgname=libdrm-git
pkgver=r5632.2978f5c
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
url="http://dri.freedesktop.org/"
license=('MIT')
depends=('libpciaccess-git' 'libsystemd')
makedepends=('git' 'xorg-util-macros-git' 'cairo>=1.12.15' 'valgrind')
provides=('libdrm')
conflicts=('libdrm')
replaces=('libdrm')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  sed -i 's/PKG_CHECK_MODULES(PTHREADSTUBS, pthread-stubs)//' configure.ac

  ./autogen.sh --prefix=/usr \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-nouveau \
      --enable-vmwgfx

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
# vim: ft=sh syn=sh et

