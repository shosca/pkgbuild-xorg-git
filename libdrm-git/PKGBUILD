# Arch libdrm Maintainers:
#Jan de Groot
#Andreas Radke

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

pkgname=libdrm-git
pkgver=r5273.6f15ca8
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
url="http://dri.freedesktop.org/"
license=('MIT')
depends=('libpciaccess-git')
makedepends=('git' 'cairo>=1.12.15' 'udev' 'valgrind')
provides=('libdrm')
conflicts=('libdrm')
replaces=('libdrm')
source=($_gitroot fix.patch)
sha256sums=('SKIP'
            '1868d44b3be091b91b18624862ed3dda48177bf2cc3254644c0f611e17d022fb')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  patch -Np1 -i ../fix.patch

  sed -i 's/PKG_CHECK_MODULES(PTHREADSTUBS, pthread-stubs)//' configure.ac

  ./autogen.sh --prefix=/usr \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-nouveau \
      --enable-vmwgfx

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
# vim: ft=sh syn=sh et

